import gringo/gringo_ast;
import gringo/gringo_pretty;
import ds/tree;
import string;
import ds/set;

export {
	gringoParse(env : GringoEnv, term : GTerm) -> GringoEnv;

	GringoEnv(
		input : string,
		names : Tree<string, GTerm>,
		// What power is each name at?
		power : Tree<string, int>,
		i : int,
		output : string,
		fail : bool
	);
}

indent = ref "";

gringoParse(env : GringoEnv, gterm : GTerm) -> GringoEnv {
	switch (gterm) {
		GBind(id, term): {
			println(^indent + "Bound " + id);
			GringoEnv(
				env with names = setTree(env.names, id, term)
			);
		}
		GChoice(left, right): {
			lenv = gringoParse(env,left);
			if (lenv.fail) {
				gringoParse(env, right);
			} else lenv
		}
		GSeq(left, right): {
			lenv = gringoParse(env, left);
			if (lenv.fail) GringoEnv(env with fail = true)
			else gringoParse(lenv, right);
		}
		GStar(term): {
			senv = gringoParse(env, term);
			if (senv.fail) {
				env;
			} else {
				gringoParse(senv, gterm);
			}
		}
		GPlus(term): {
			senv = gringoParse(env, term);
			if (senv.fail) {
				println(^indent + "Could not parse plus");
				senv;
			} else {
				gringoParse(senv, GStar(term));
			}
		}
		GOpt(term): {
			senv = gringoParse(env, term);
			if (senv.fail) {
				env;
			} else {
				senv;
			}
		}
		GNegate(term): {
			senv = gringoParse(env, term);
			if (senv.fail) {
				GringoEnv(senv with fail = false);
			} else {
				println(^indent + "Do not accept term");
				GringoEnv(env with fail = true);
			}
		}
		GUnquote(term): {
			un = unquoteGTerm(env, term);
			GringoEnv(
				env with output = env.output + un
			)
		}
		GString(text): {
			if (strContainsAt(env.input, env.i, text)) {
				GringoEnv(
					env with i = env.i + strlen(text)
				)
			} else {
				println(^indent + "Could not parse '" + text + "'");
				GringoEnv(env with fail = true)
			}
		}
		GRange(lower, upper): {
			code = getCharCodeAt(env.input, env.i);
			if (lower <= code && code <= upper) {
				// println(^indent + "Parsed " + getCharAt(env.input, env.i));
				GringoEnv(
					env with i = env.i + 1
				)
			} else {
				GringoEnv(env with fail = true)
			}
		}
		GVar(id, power): {
			// If the power of this id is <= the power in our tree, abort
			mterm = lookupTree(env.names, id);
			mterm ?? {

				oindent = ^indent;
				indent := oindent + "  ";
				termPower = lookupTreeDef(env.power, id, -1);

				println(oindent + "Parse " + id + " with i=" + i2s(env.i) + " with power " + i2s(power) + " in scope " + i2s(termPower));

				rec = if (power == termPower) {
					env;
				} else if (power < termPower) {
					GringoEnv(env with fail = true);
				} else {
					nenv = GringoEnv(env with power = setTree(env.power, id, power));
					gringoParse(nenv, mterm);
				};

				indent := oindent;

				if (rec.fail) {
					println(oindent + "FAILED " + id);
					GringoEnv(env with fail = true);
				} else {
					println(oindent + "SUCCESS " + id + " at " + i2s(rec.i) + " with " + rec.output);
					rec;
				}

			} : {
				println(^indent + "Unknown name");
				GringoEnv(env with fail = true);
			}
		}
	}
}

unquoteGTerm(env : GringoEnv, term : GTerm) -> string {
	switch (term) {
		GString(text): text;
		GVar(id, power): {
			mbind = lookupTree(env.names, id);
			mbind ?? {
				unquoteGTerm(env, mbind)
			} : {
				println("Could not unquote " + id);
				"";
			}
		}
		default: {
			println("Could not unquote");
			"";
		}
	}
}
