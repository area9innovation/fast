import gringo/gringo_ast;
import string;

export {
	gterm2string(g : GTerm) -> string;
}

gterm2string(g : GTerm) -> string {
	switch (g) {
		GBind(id, term): "\n" + id + " = " + gterm2string(term) + ";";
		GChoice(left, right): gterm2string(left) + "\n | " + gterm2string(right);
		GSeq(left, right): gterm2string(left) + " " + gterm2string(right);
		GStar(term): gterm2string(term) + "*";
		GPlus(term): gterm2string(term) + "+";
		GOpt(term): gterm2string(term) + "?";
		GNegate(term): "!" + gterm2string(term);
		GUnquote(term): "$" + gterm2string(term);
		GString(text): {
			if (text == "\"") "'\"'"
			else toString(text);
		}
		GRange(lower, upper): {
			c = \code -> {
				if (code < 0x20 || code > 0xff) {
					"0x" + formatHex(code)
				} else fromCharCode(code)
			}
			"'" + c(lower) + "'-'" + c(upper) + "'"
		}
		GVar(id, power): id + if (power != 0) "(" + i2s(power) + ")" else "";
	}
}
