import gringo/jingo/utils;
import ds/tree;
import string;
import math/math;

export {
	getJingoBuiltins() -> Tree<string, (stack : List<Jingo<?>>) -> List<Jingo<?>>>;
}

getJingoBuiltins() -> Tree<string, (stack : List<Jingo<?>>) -> List<Jingo<?>>> {
	pairs2tree([
		// x drop ->
		Pair("drop", \stack -> {
			elm = popJStack(stack);
			elm.second;
		}),

		// x dup -> x x
		Pair("dup", \stack -> {
			elm = popJStack(stack);
			pushJStack(stack, elm.first);
		}),

		// x print ->
		Pair("print", \stack -> {
			elm = popJStack(stack);
			println(elm);
			elm.second;
		}),

		// x y swap -> y x
		Pair("swap", \stack -> {
			y = popJStack(stack);
			x = popJStack(y.second);
			ns = pushJStack(x.second, y.first);
			pushJStack(ns, x.first);
		}),

		// x y + -> x+y
		Pair("+", \stack -> {
			y = popJStack(stack);
			x = popJStack(y.second);
			if (isJInt(x.first) && isJInt(y.first)) {
				pushJStack(x.second, JInt(getJInt(x.first) + getJInt(y.first)));
			} else if (isJString(x.first) && isJString(y.first)) {
				pushJStack(x.second, JString(getJString(x.first) + getJString(y.first)));
			} else {
				println("Can only add ints and strings");
				stack;
			}
		}),

		// x y - -> x-y
		Pair("-", \stack -> {
			y = popJStack(stack);
			x = popJStack(y.second);
			pushJStack(x.second, JInt(getJInt(x.first) - getJInt(y.first)));
		}),

		// x y * -> x*y
		Pair("*", \stack -> {
			y = popJStack(stack);
			x = popJStack(y.second);
			pushJStack(x.second, JInt(getJInt(x.first) * getJInt(y.first)));
		}),

		// x y / -> x/y
		Pair("/", \stack -> {
			y = popJStack(stack);
			x = popJStack(y.second);
			pushJStack(x.second, JInt(getJInt(x.first) / getJInt(y.first)));
		}),

		// x y % -> x%y
		Pair("%", \stack -> {
			y = popJStack(stack);
			x = popJStack(y.second);
			pushJStack(x.second, JInt(getJInt(x.first) % getJInt(y.first)));
		}),

		// <string> length -> <int>
		Pair("length", \stack -> {
			x = popJStack(stack);
			st = getJString(x.first);
			pushJStack(x.second, JInt(strlen(st)));
		}),

		// <string> <int> get -> <string>
		Pair("get", \stack -> {
			i = popJStack(stack);
			x = popJStack(i.second);
			st = getJString(x.first);
			pushJStack(x.second, JString(getCharAt(st, getJInt(i.first))));
		}),

		// <string> <int> get -> <int>
		Pair("getcode", \stack -> {
			i = popJStack(stack);
			x = popJStack(i.second);
			st = getJString(x.first);
			pushJStack(x.second, JInt(getCharCodeAt(st, getJInt(i.first))));
		}),

		// <string> s2i -> <int>
		Pair("s2i", \stack -> {
			x = popJStack(stack);
			st = getJString(x.first);
			pushJStack(x.second, JInt(s2i(st)));
		}),

		// <int> i2s -> <string>
		Pair("i2s", \stack -> {
			x = popJStack(stack);
			st = getJInt(x.first);
			pushJStack(x.second, JString(i2s(st)));
		}),
	])
}
