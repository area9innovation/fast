import gringo/gringo_ast;
import runtime;

export {
	gringoGrammar() -> GTerm;
}

gringoGrammar() -> GTerm {
	ws = GVar("ws", 0);
	GRule("term", 
		gchoices([
			// term(1) "|" term(2)	// Choice
			gseq([GVar("term", 1), GString("|"), ws, GVar("term", 2), 
				GUnquote(GString("GChoice"))
			]),
			// term(10) term(11)		// Sequence
			gseq([GVar("term", 10), GVar("term", 11), 
				GUnquote(GString("GSeq"))
			]),
			// term(12) "*"			// 0 or more
			gseq([GVar("term", 12), GString("*"), ws, 
				GUnquote(GString("GStar"))
			]),
			// term(12) "+"			// One or more
			gseq([GVar("term", 12), GString("+"), ws, 
				GUnquote(GString("GPlus"))
			]),
			// term(12) "?"			// Optional
			gseq([GVar("term", 12), GString("?"), ws, 
				GUnquote(GString("GOpt"))
			]),
			//	| term(13) ":" type		// Type annotation
			// "!" term(0)			// Negation
			gseq([GString("!"), ws, GVar("term", 0), 
				GUnquote(GString("GNegate"))
			]),
			// "(" term(0) ")" 		// Grouping
			gseq([GString("("), ws, GVar("term", 0), GString(")"), ws]),
			// "$" unquote
			gseq([GString("$"), ws, GVar("term", 0), 
				GUnquote(GString("GUnquote"))
			]),

			// string				// Constant string
			gseq([GString("\""), GUnquote(GStar(
					GSeq(
						GNegate(GString("\"")),
						GRange(0, 0xffff)
					)
				)),
				GString("\""), ws,
				GUnquote(GString("GString"))
			]),
			// char "-" char			// Range
			gseq([GString("'"), GVar("char", 0), GString("'"), GString("-"), GString("'"), GVar("char", 0), GString("'"), ws]),
			// id "=" term(0) ";"	// Binding
			gseq([GVar("id", 0), ws, GString("="), ws, GVar("term", 1), GString(";"), ws, GVar("term", 2), 
				GUnquote(GString("GBind"))
			]),
			// id "(" int ")"		// Rule ref with power
			gseq([GVar("id", 0), ws, GString("("), ws, GVar("int", 0), ws, GString(")"), ws, 
				GUnquote(GString("GVar"))
			]),
			// id					// Rule ref
			gseq([GVar("id", 0), ws, 
				GUnquote(GString("0")),
				GUnquote(GString("GVar"))
			])
		]),

		// id: 'a'-'z'+
		GRule("id", GUnquote(GPlus(
			GRange(97, 122),
		)),

		// int: '0'-'9'+
		GRule("int", GUnquote(GPlus(
			GRange(48, 57)
		)),
		GRule("char", GChoice(GSeq(GString("0x"), GPlus(GVar("hexdigit", 0))), GRange(0, 0xffff)),

		GRule("hexdigit", gchoices([
			GRange(65, 70),
			GRange(97, 102),
			GRange(48, 57)
		]),

		GRule("ws", GStar(GVar("s", 0)),

		GRule("s", gchoices([GString(" "), GString("\t"), GString("\n")]),

		GRule("grammar", 
			gseq([
				GVar("ws", 0),
				GVar("term", 0)
			]),

		// Parse a term!
		GVar("grammar", 0)
		
		))))))
	));
}
