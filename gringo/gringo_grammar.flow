import gringo/gringo_ast;
import runtime;

export {
	gringoGrammar() -> GTerm;
}

gringoGrammar() -> GTerm {
	ws = GVar("ws");
	GRule("term", 
		gprecedences([
			// term "|>" term	// Choice
			gseq([GVar("term"), GString("|>"), ws, GVar("term"), 
				GUnquote(GString("GPrecedence"))
			]),
			// term "|" term	// Choice
			gseq([GVar("term"), GString("|"), ws, GVar("term"), 
				GUnquote(GString("GChoice"))
			]),
			// term term		// Sequence
			gseq([GVar("term"), GVar("term"), 
				GUnquote(GString("GSeq"))
			]),
			// term "*"			// 0 or more
			gseq([GVar("term"), GString("*"), ws, 
				GUnquote(GString("GStar"))
			]),
			// term "+"			// One or more
			gseq([GVar("term"), GString("+"), ws, 
				GUnquote(GString("GPlus"))
			]),
			// term "?"			// Optional
			gseq([GVar("term"), GString("?"), ws, 
				GUnquote(GString("GOpt"))
			]),
			//	| term ":" type		// Type annotation
			// "!" term			// Negation
			gseq([GString("!"), ws, GVar("term"), 
				GUnquote(GString("GNegate"))
			]),
			// "(" term ")" 		// Grouping
			gseq([GString("("), ws, GVar("term"), GString(")"), ws]),
			// "$" term
			gseq([GString("$"), ws, GVar("term"), 
				GUnquote(GString("GUnquote"))
			]),

			// string				// Constant string
			gseq([GString("\""), GUnquote(GStar(
					GSeq(
						GNegate(GString("\"")),
						GRange(0, 0xffff)
					)
				)),
				GString("\""), ws,
				GUnquote(GString("GString"))
			]),
			// char "-" char			// Range
			gseq([GString("'"), GUnquote(GVar("char")), GString("'"), GString("-"), 
				GString("'"), GUnquote(GVar("char")), GString("'"), ws, 
				GUnquote(GString("GRange"))
			]),
			// id "=" term ";"	// Binding
			gseq([GVar("id"), ws, GString("="), ws, GVar("term"), GString(";"), ws, GVar("term"), 
				GUnquote(GString("GRule"))
			]),
			// id					// Rule ref
			gseq([GVar("id"), ws, 
				GUnquote(GString("GVar"))
			])
		]),

		// id: 'a'-'z'+
		GRule("id", GUnquote(GPlus(
			GRange(97, 122),
		)),

		// int: '0'-'9'+
		GRule("int", GUnquote(GPlus(
			GRange(48, 57)
		)),
		GRule("char", GChoice(GSeq(GString("0x"), GPlus(GVar("hexdigit"))), GRange(0, 0xffff)),

		GRule("hexdigit", gchoices([
			GRange(65, 70),
			GRange(97, 102),
			GRange(48, 57)
		]),

		GRule("ws", GStar(GVar("s")),

		GRule("s", gchoices([GString(" "), GString("\t"), GString("\n")]),

		GRule("grammar", 
			gseq([
				GVar("ws"),
				GVar("term")
			]),

		// Parse a term!
		GVar("grammar")
		
		))))))
	));
}
