import gringo/gringo_ast;
import gringo/utils;
import math/math;

export {
	/*
		e = e tail e |> rest;
		->
		e = e1 tail e1 | e1;
		e1 = rest;
	*/
	expandPrecedence(g : GTerm) -> GTerm;
}

expandPrecedence(g : GTerm) -> GTerm {
	switch (g) {
		GRule(id, term, body): {
			expandPrecedenceInRule(id, term, expandPrecedence(body), 0);
		}
		default: g;
	}
}

// e = <term> -> e0 = <term>
expandPrecedenceInRule(id : string, term : GTerm, body : GTerm, level : int) -> GTerm {
	switch (term) {
		GPrecedence(left, right): {
			newid = id + (if (level == 0) "" else i2s(level));
			nextid = id + i2s(level + 1);
			nleft = replaceLeftRecursion(GVar(id), GVar(nextid), GVar(newid), left);
			GRule(newid, 
				GChoice(
					nleft, 
					GVar(nextid)
				),
				expandPrecedenceInRule(id, right, body, level + 1)
			)
		}
		default: {
			newid = id + (if (level == 0) "" else i2s(level));
			GRule(newid, term, 
				body
			)
		}
	}
}


// Does basic replacement in the term
replaceLeftRecursion(find : GTerm, replaceLeft : GTerm, replaceRest : GTerm, g : GTerm) -> GTerm {
	if (g == find) replaceLeft
	else switch (g) {
		GRule(id, term, body): {
			nbody = replaceLeftRecursion(find, replaceLeft, replaceRest, body);
			nterm = replaceLeftRecursion(find, replaceLeft, replaceRest, term);
			GRule(id, nterm, nbody);
		}
		GPrecedence(left, right): GPrecedence(replaceLeftRecursion(find, replaceLeft, replaceRest, left), replaceLeftRecursion(find, replaceLeft, replaceRest, right)); 
		GChoice(left, right): GChoice(replaceLeftRecursion(find, replaceLeft, replaceRest, left), replaceLeftRecursion(find, replaceLeft, replaceRest, right)); 
		GSeq(left, right): {
			GSeq(
				replaceLeftRecursion(find, replaceLeft, replaceRest, left),
				// OK, we use the other replacement for the rest
				replaceLeftRecursion(find, replaceRest, replaceRest, right), 
			);
		}
		GStar(term): GStar(replaceLeftRecursion(find, replaceLeft, replaceRest, term));
		GPlus(term): GPlus(replaceLeftRecursion(find, replaceLeft, replaceRest, term));
		GOpt(term): GOpt(replaceLeftRecursion(find, replaceLeft, replaceRest, term));
		GNegate(term): GNegate(replaceLeftRecursion(find, replaceLeft, replaceRest, term));
		GUnquote(term): GUnquote(replaceLeftRecursion(find, replaceLeft, replaceRest, term));
		GString(text): g;
		GRange(lower, upper): g;
		GVar(id): g;
		GEpsilon(): g;
	}
}

getGSequence(g : GTerm) -> [GTerm] {
	switch (g) {
		GSeq(l, r): concat(getGSequence(l), getGSequence(r));
		default: [g];
	}
}
