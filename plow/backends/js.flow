import plow/backends/bprogram;
import plow/backends/back_eval;
import plow/backends/bprogram_lower;
import plow/backends/js_natives;
import plow/backends/pretty;
import plow/driver;

export {
	bmodule2js(cache : PlowCache, flowpath : string) -> string;
	bprogram2js(b : BProgram) -> string;
}

bmodule2js(cache : PlowCache, flowpath : string) -> string {
	modules = list2array(programBModules(cache, flowpath, makeList(), makeSet()).first);
	bexp_program = bmodules2BExpProgram(modules);
	b_program = bexpProgram2BProgram(bexp_program);
	bprogram2js(b_program);
}

programBModules(cache : PlowCache, file : string, acc : List<BModule>, visited : Set<string>) -> Pair<List<BModule>, Set<string>> {
	module = getCachedBModule(cache.modules, file);
	imported_visited = fold(module.imports, Pair(acc, insertSet(visited, file)), \ac, imp -> {
		if (containsSet(ac.second, imp.path)) ac else {
			programBModules(cache, imp.path, ac.first, ac.second)
		}
	});
	Pair(Cons(module, imported_visited.first), imported_visited.second);
}

empty_BModule = BModule("", "", [], [], DExport(makeSet(), makeSet(), makeSet(), -1), makeTree(), makeTree(), makeTree(), makeTree(), makeTree(), []);

mergeBModules(b1 : BModule, b2 : BModule) -> BModule {
	BModule("", "", [], [],
		DExport(
			mergeSets(b1.exported.exportedTypeNames, b2.exported.exportedTypeNames),
			mergeSets(b1.exported.exportedTypes, b2.exported.exportedTypes),
			mergeSets(b1.exported.exportedGlobals, b2.exported.exportedGlobals), -1
		),
		mergeTree(b1.structs, b2.structs),
		mergeTree(b1.unions, b2.unions),
		mergeTree(b1.fields, b2.fields),
		mergeTree(b1.natives, b2.natives),
		mergeTree(b1.globals, b2.globals),
		concat(b1.order, b2.order)
	);
}

bprogram2js(b : BProgram) -> string {
	backend = jsBackend();
	structs = superglue(getTreeValues(b.structs), \s : DStruct -> dstruct2js(backend, s), "\n");
	body = superglue(b.declarations, \d : BDeclaration -> bdeclaration2js(backend, d), "\n");

	getBackendRequirements(backend)
	+ structs + "\n"
	+ body
	+ "\nmain()";
}

dstruct2js(backend : Backend, s : DStruct) -> string {
	blueprint("
		function %id%(%args%) {
			%body%
		}", [
		"id", s.id,
		"args", superglue(s.structArgs, \arg -> arg.id, ", "),
		"body", superglue(s.structArgs, \arg -> "this." + arg.id + " = " + arg.id + ";", "\n")
	]);
}

bdeclaration2js(backend : Backend, b : BDeclaration) -> string {
	switch (b) {
		BStatFunction(name, args, body, tailcall, __): {
			if (tailcall) {
				blueprint("
					function %name%(%args%) {
						T:  while(true) {
							%body%
						}
					}", [
					"name", name,
					"args", strGlue(args, ", "),
					"body", bstatement2js(backend, body, true)
				]);
			} else {
				blueprint("
					function %name%(%args%) {
						%body%
					}", [
					"name", name,
					"args", strGlue(args, ", "),
					"body", bstatement2js(backend, body, bstatementType(body) != bTypeVoid)
				]);
			}
		}
		BGlobalVar(name, body, __): "var " + name + " = " + bexp2js(backend, body) + ";";
		BNativeDec(name, isIo, type, nativeName, mfallback): {
			generated = produceBackendNative(backend, nativeName);
			if (generated == "") {
				mfallback ?? {
					bdeclaration2js(backend, mfallback);
				}: {
					backend.onError(0, "native " + nativeName + " not implemented");
					nativeName
				}
			} else {
				"var " + name + " = " + generated + ";"
			}
		}
	}
}

bstatement2js(backend : Backend, b : BStatement, withReturn : bool) -> string {
	switch (b) {
		BIfStatement(cond, then, else_): {
			if (!withReturn && else_ == BReturnVoid()) {
				blueprint("
					if (%cond%) {
						%then%
					}", [
					"cond", bexp2js(backend, cond),
					"then", bstatement2js(backend, then, withReturn),
				])
			} else {
				blueprint("
					if (%cond%) {
						%then%
					} else {
						%else%
					}", [
					"cond", bexp2js(backend, cond),
					"then", bstatement2js(backend, then, withReturn),
					"else", bstatement2js(backend, else_, withReturn),
				])
			}
		}
		BLetStatement(name, value, body): {
			blueprint("
				var %name% = %value%;
				%body%", [
					"name", name,
					"value", bexp2js(backend, value),
					"body", bstatement2js(backend, body, withReturn),
			])
		}
		BReturnVoid(): if (withReturn) "return;" else "";
		BReturn(val): {
			(if (withReturn) "return " else "") + bexp2js(backend, val) + ";";
		}
		BTailCall(fn, args, values, type): {
			/*
				var $a_=((start+1)|0);
				start=$a_;
				continue T*/
			if (length(args) == 1) {
				blueprint("
					%arg% = %value%;
					continue T;
				", ["arg", args[0],
					"value", bexp2js(backend, values[0])])
			} else {
				// Define tmps
				supergluei(values, \i, value -> {
					"var $a" + i2s(i) + "=" + bexp2js(backend, value)
				}, ";")
				+ ";" + supergluei(args, \i, arg -> {
					arg + "=" + "$a" + i2s(i);
				}, ";")
				+ ";continue T";
			}
		}
		BSequenceStatement(statements): {
			supergluei(statements, \i, stat -> {
				bstatement2js(backend, stat, withReturn && i == length(statements) - 1 && bstatementType(stat) != bTypeVoid)
			}, "\n");
		}
		BSwitchStatement(val, cases, type): {
			blueprint("
				switch (%val%) {
					%cases%
				}", [
					"val", bexp2js(backend, val),
					"cases", superglue(cases, \c -> {
						(if (c.id == "default") c.id else "case " + c.id)
						 + ": " + bstatement2js(backend, c.body, withReturn)
					}, "\n"),
			])
		}
		BSetMutableStatement(left, field, value): {
			bexp2js(backend, left) + "." + field + "=" + bexp2js(backend, value) + ";";
		}
	}
}

bexp2js(backend : Backend, b : BExp) -> string {
	switch (b) {
		BValue(): bvalue2js(b);
		BVar(name, __): name;
		BLet(name, value, body, type): {
			blueprint("
				function() {
				var %name% = %value%;
				%body%
				}()", [
					"name", name,
					"value", bexp2js(backend, value),
					"body", bstatement2js(backend, bexp2BReturn(body), type != bTypeVoid),
			])
		}
		BLambda(args, body, __): {
			"function(" 
			+ strGlue(args, ", ") + ") {" 
			+ bstatement2js(backend, bexp2BReturn(body), bexpType(body) != bTypeVoid)
			+ "}"
		};
		BCall(fn, args, restype): {
			produceBackendCall(backend, b, bexp2js);
		}
		BIf(cond, then, else_, __): "(" + bexp2js(backend, cond) + " ? " + bexp2js(backend, then) + " : " + bexp2js(backend, else_) + ")";
		BSequence(statements, __): {
			if (statements == []) {
				"null"
			} else {
				"(" + superglue(statements, \s -> bexp2js(backend, s), ", ") + ")";
			}
		}
		BSwitch(val, __, cases, __, type): {
			"function() {" 
			+ bstatement2js(backend, bexp2BReturn(b), type != bTypeVoid)
			+ "}()"
		}
		BConstruct(struct, args, type): {
			"new " + struct + "(" + superglue(args, \arg -> bexp2js(backend, arg), ", ") + ")";
		}
		BField(value, field, type): {
			bexp2js(backend, value) + "." + field;
		}
		BSetMutable(left, field, value, type): {
			bexp2js(backend, left) + "." + field + "=" + bexp2js(backend, value);
		}
		BArray(args, type): {
			"[" + superglue(args, \arg -> bexp2js(backend, arg), ", ") + "]";
		}
	}
}

bvalue2js(b : BValue) -> string {
	switch (b) {
		BVoid(): "null";
		BBool(v): b2s(v);
		BInt(v):  i2s(v);
		BDouble(value): double2string(value);
		BString(value): toString(value);
	}
}

// Still not decided whether this is required or not
double2string(d : double) -> string {
	v0 = d2s(d);
	if (strContains(v0, ".") 
		|| strContains(v0, "e") 
		|| strContains(v0, "E") 
		|| dfloor(d) == d) {
		v0
	} else v0 + ".0";
}
