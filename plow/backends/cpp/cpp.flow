import plow/backends/link;
import plow/backends/back_eval;
import plow/backends/pretty;
import plow/backends/cpp/cpp_backend;
import plow/backends/type_hierarchy;
import plow/backends/utils;

/*

flowcpp plow\plow.flow -- file=demos/euler/euler1.flow cpp=euler1.cpp
g++ -std=c++2a euler1.cpp 2>out.flow

*/

export {
	// Compiles the given flow path to C++ as a single string
	bmodule2cpp(cache : PlowCache, flowpath : string, shortenIds : bool) -> string;
}

bmodule2cpp(cache : PlowCache, flowpath : string, shortenIds : bool) -> string {
	bprogram2cpp(linkBProgram(cache, flowpath, shortenIds, cppBackendSpec()), flowpath);
}

bprogram2cpp(b : BProgram, flowpath : string) -> string {
	typeHierarcy = resolveBTypeHierarchy(b.structs, b.unions);

	backend = cppBackend();
	// Structs in unions
	unionStructs = foldTree(typeHierarcy.third, "", \id, struct, acc -> {
		acc + dstruct2cpp(backend, struct) + "\n"
	});
	unions = superglue(typeHierarcy.first, \u : DUnion -> dunion2cpp(backend, u), "\n");
	structs = superglue(typeHierarcy.second, \s : DStruct -> "struct " + dstruct2cpp(backend, s), "\n");
	body = superglue(b.declarations, \d : BDeclaration -> bdeclaration2cpp(backend, d), "\n");

	// The backend keeps track of what requirements are used by the various
	// constructs used
	strReplace(
		"#include <cstdint>\n#include <variant>\n#include <functional>\n#include <any>\n#include <tuple>"
		+ getBackendRequirements(backend)
	//	+ "// RUNTIME\n"
	//	+ runtime + "\n"
		+ "// Structs for unions\n"
		+ unionStructs
		+ "// UNIONS\n"
		+ unions + "\n"
		+ "// STRUCTS\n"
		+ structs + "\n"
		+ "// BODY\n"
		+ body
		+ "int main() { return int(flow_main()); }",
		"\t",
		"  "
	)
}

bdeclaration2cpp(backend : Backend, d : BDeclaration) -> string {
	switch (d) {
		BGlobalVar(id, body, type): {
			btype2cpp(backend, type) + " " + id + " = " + bexp2cpp(backend, body) + ";";
		}
		BStatFunction(id, args, body, tailCall, type): {
			if (id == "flow_i2s") {
				blueprint("
					std::u16string flow_i2s(int32_t flow_i) {
						std::to_string(flow_i)
					}
				", [
				])
			} else if (id == "flow_i2d") {
				blueprint("
					double flow_i2d(int32_t flow_i) {
						double(flow_i)
					}
				", [
				])
			} else {
				polyvars = extractPolymorphism(makeSet(), type);
				argtypes = getBArgTypes(type);
				rt = btype2cpp(backend, getBReturnType(type));
			
				// TODO: We only need "div" if this fn is recursive
				blueprint("
					%polyvars%%rt% %id%(%args%) {
						%body%
					}
				", [
					"polyvars", {
						if (isEmptySet(polyvars)) ""
						else "template <" + superglue(set2array(polyvars), \pv -> "typename " + btype2cpp(backend, pv), ", ") + "> "
					},
					"id", id,
					// TODO: Add types and typars
					"args", supergluei(args, \i, arg -> {
						btype2cpp(backend, argtypes[i]) + " " + arg
					}, ", "),
					"rt", rt,
					"body", bstatement2cpp(backend, body)
				])
			}
		}
		BExpNative(id, isio, type, nativeName): {
			"// TODO: Native " + id;
		}
	}
}

bstatement2cpp(backend : Backend, s : BStatement) -> string {
	todo = \ -> {
		println("TODO C++ statement: " + toString(s));
		toString(s)
	}
	rec = \ss -> bstatement2cpp(backend, ss);
	rece = \ee -> bexp2cpp(backend, ee);
	switch (s) {
		BIfStatement(cond, then, else_): {
			blueprint("
					if (%c%) {
						%then%
					} else {
						%else%
					}",
				[
					"c", rece(cond),
					"then", rec(then),
					"else", rec(else_),
			]);

		}
		BLetStatement(id, value, body): {
			"auto " + id + " = " + bexp2cpp(backend, value) + ";\n"
			+ bstatement2cpp(backend, body);
		}
		BSetMutableStatement(left, field, value): todo();
		BReturnVoid(): "return;";
		BReturn(val): {
			type = getBType(val);
			if (type == bvoidType) {
				""
			} else {
				"return ";
			}  + bexp2cpp(backend, val) + ";"
		}
		BTailCall(fn, args, values, type): {
			fn  + "(" +superglue(values, rece, ", ") + ");"
		}
		BSequenceStatement(statements): {
			blueprint("
					{
						%exps%
					}",
				[
					"exps", superglue(statements, rec, "\n"),
			]);

		}
		BSwitchStatement(expr, cases, type): todo();
	}
}


bexp2cpp(backend : Backend, bexp : BExp) -> string {
	rec = \ee -> bexp2cpp(backend, ee);
	switch (bexp) {
		BVoid(): "{}";
		BBool(v): if (v) "true" else "false";
		BInt(v): i2s(v);
		BDouble(v): double2string(v);
		BString(v): toString(v);
		BVar(id, type): id;
		BLet(id, value, body, type): {
			// TODO: Find a list of globals here
			globals = makeSet();
			freevars0 = extractFreeNamesBExp(makeSet(), globals, value);
			freevars = set2array(removeSet(extractFreeNamesBExp(freevars0, globals, body), id));
			blueprint("
				[%capture%]() {
					auto %id% = %value%;
					return %body%;
				}()", [
				"id", id,
				"value", rec(value),
				"body", rec(body),
				"capture", strGlue(freevars, ",")
			])
		}
		BIf(cond, then, else_, type): {
			blueprint("
					(%c%) ? (
						%then%
					) : (
						%else%
					)",
				[
					"c", rec(cond),
					"then", rec(then),
					"else", rec(else_),
			]);
		}
		BCall(fn, args, type): {
			produceBackendCall(backend, bexp, bexp2cpp);
		}
		BLambda(args, body, type): {
			argtypes = getBArgTypes(type);
			rt0 = btype2cpp(backend, getBReturnType(type));
			blueprint("
					[](%args%) {
						return %body%;
					}",
				[
					"args", supergluei(args, \i, arg -> {
							btype2cpp(backend, argtypes[i]) + " " + arg
						}, ", "),
					"body", rec(body),
			]);
		}
		BSequence(exps, type): {
			blueprint("
					(%exps%)",
				[
					"exps", superglue(exps, rec, ", "),
			]);
		}
		BConstruct(struct, args, type): {
			blueprint("(struct %struct%){%args%}",
				[
					"struct", struct,
					"args", superglue(args, rec, ", "),
			]);
		}
		BField(value, field, type): {
			blueprint("
					%value%.%field%",
				[
					"value", rec(value),
					"field", field,
			]);
		}
		BSetMutable(left, field, value, type): "TODO-set-mutable(" + rec(left) + "." + field + ", " + rec(value);
		BSwitch(value, switchType, cases, defaultCase, type): {
			/*
			match(m) {
				Nothing -> 0
				Just(i) -> i
				_ -> 2
			}
			*/
			blueprint("
					switch (%value%.index()) {
						%cases%
						%default%
					}",
				[
					"value", rec(value),
					"cases", superglue(cases, \case -> {
						blueprint("
							case %id%: {
								%body%
							}",
							[
								"id", case.id, // TODO: This should be the int id
								"body", rec(case.body)
							]);
					}, ";\n"),
					"default", defaultCase ?? ("default: " + rec(defaultCase)) : "",
			]);
		}
		BArray(args, type): {
			blueprint("%type%({ %args% })",
				[
					"type", btype2cpp(backend, type),
					"args", superglue(args, rec, ", "),
			]);
		}
	}
}

dunion2cpp(backend : Backend, u : DUnion) -> string {
	blueprint("
		using %name% = std::variant<%members%>;", [
			"name", u.id,
			"members", superglue(u.types, \t : DTypeName -> {
				dtype2cpp(backend, t);
			}, "\n")
		]
	);
}

dstruct2cpp(backend : Backend, s : DStruct) -> string {
	"struct " + s.id + "{"
	+ superglue(s.structArgs, \sa -> {
		dtype2cpp(backend, sa.type) + " " + sa.id + ";"
	}, "; ") + "};";
}


dtype2cpp(backend : Backend, type : DType) -> string {
	btype2cpp(backend, dtype2btype(type));
}

btype2cpp(backend : Backend, btype : BType) -> string {
	switch (btype) {
		BTypePar(id): {
			// In C++, we use a, b, c, ...
			len = strlen(id) - 1;
			fromCharCode(getCharCodeAt("A", 0) + len);
		}
		BTypeName(id, typars): {
			if (id == "void") "void"
			else if (id == "bool" || id == "double") id
			else if (id == "int") { "int32_t" }
			else if (id == "string") { "std::u16string" }
			else if (id == "flow") "std::any"
			else if (id == "array") {
				"std::vector<" + btype2cpp(backend, typars[0]) + ">"
			} else if (id == "ref") {
				"std::tuple<" + btype2cpp(backend, typars[0]) + ">"
			} else if (trim2(id, "?") == "") {
				btype2cpp(backend, BTypePar(id));
			} else {
				id
				+ (if (typars != []) {
					"<" + superglue(typars, \tp -> btype2cpp(backend, tp), ", ") + ">"
				} else "")
			}
		}
		BTypeFunction(args, returnType): {
			"std::function<" + btype2cpp(backend, returnType)
			+ "(" + superglue(args, \tp -> btype2cpp(backend, tp), ", ") + ")>"
		}
	}
}
