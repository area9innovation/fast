import plow/dexp/dexp;
import plow/types/typeof;
import plow/types/pretty;
import string;
import text/blueprint;

export {
	prettyDModule(d : DModule) -> void;
	prettyDExp(input : string, i : string, d : DExp) -> void;
}

prettyDModule(d : DModule) -> void {
	input = getFileContent(d.fullpath);
	println(d.flowpath + " (" + d.fullpath + "):");

	iter(d.imports, \i : DImport -> {
		println("  import " + i.path);
	});
	iter(d.forbids, \f : DForbid -> {
		println("  forbid " + f.path);
	});
	// TODO:
	//	exported : DExport, 
	traverseInOrder(d.structs, \sid, s : DStruct -> {
		println("  struct " + prettyDStruct(input, s));
	});
	//	unions : Tree<string, DUnion>,
	//	typedefs : Tree<string, DTypeDef>,
	//	natives : Tree<string, DNative>,
	iter(d.order, \id -> {
		mglobal = lookupTree(d.globals, id);
		mglobal ?? {
			println(" " + id + "<" + strGlue(mglobal.typars, ", ") + ">" + "=");
			prettyDExp(input, "  ", mglobal.value);
		} : {}
	});
}


prettyDExp(input : string, i : string, d : DExp) -> void {
	rec = \e -> prettyDExp(input, i + " ", e);
	type = \t -> println(i + " " + prettyDType(input, t));
	pr = \s : string -> {
		line = if (d.pos == -1) "MISSING POS " + d.structname else substring(input, d.pos, 20);
		pre = i + s;
		typ = prettyTType(dexpType(TTypeEnv(makeTree(), \ -> 0), d));
		println(rpad(pre, " ", 60) + " " + rpad(typ, " ", 40) + " " + takeBefore(line, "\n", line));
	}
	switch (d : DExp) {
		DVoid(pos): pr("void");
		DBool(v, pos): pr("bool");
		DInt(v, pos): pr("int");
		DDouble(v, pos): pr("double");
		DString(v, pos): pr("string");
		DStringInclude(path, pos): {
			pr("String include " + path);
		}
		DLambda(args, body, pos, tyvar): {
			pr("lambda " + superglue(args, \a : DArgType -> (if (a.id != "") a.id + ":" else "ERROR") + prettyDType(input, a.type), ","));
			rec(body);
		}
		DVar(var, pos, tyvar): {
			pr("'" + var + "'");
		}
		DLet(var, value, body, pos, tyvar): {
			pr("let " + var);
			rec(value);
			rec(body);
		}
		DIf(cond, then, else_, pos, tyvar): {
			pr("if");
			rec(cond);
			rec(then);
			rec(else_);
		}
		DCall(fn, args, pos, tyvar): {
			pr("call");
			rec(fn);
			iter(args, rec);
		}
		DCallOp(op, args, pos, tyvar): {
			pr("op: " + op);
			iter(args, rec);
		}
		DRef(value, pos, tyvar): {
			pr("ref");
			rec(value);
		}
		DField(value, field, pos, tyvar): {
			pr("field ." + field);
			rec(value);
		}
		DSetMutable(lhs, field, value, pos, tyvar): {
			pr("set mutable field" + field);
			rec(lhs);
			rec(value);
		}
		DArray(exps, pos, tyvar): {
			pr("array");
			iter(exps, rec);
		}
		DSequence(exps, pos, tyvar): {
			pr("sequence");
			iter(exps, rec);
		}
		DSwitch(value, typ, cases, mdef, pos, tyvar): {
			pr("switch");
			rec(value);
			type(typ);
			iter(cases, \case : DCase -> {
				pr("  case " + case.id);
				prettyDExp(input, i + "    ", case.body);
			});
			mdef ?? {
				pr("  default:");
				rec(mdef)
			} : {}
		}
		DCast(value, target, pos): {
			pr("cast");
			rec(value);
			type(target);
		}
		DTyped(value, typ, pos): {
			pr("typed : " + prettyDType(input, typ));
			rec(value);
		}
		DWith(struct, value, fields, pos, tyvar): {
			pr("with " + struct);
			rec(value);
			// iter(fields, rec);
		}
		DConstruct(structid, args, pos, tyvar): {
			pr("construct " + structid);
			iter(args, rec);
		}
		DDowncast(value, subtype, scope, pos, tyvar): {
			pr("downcast");
			rec(value);
			type(subtype);
			rec(scope);
		}
	}
}

prettyDType(input : string, d : DType) -> string {
	switch (d) {
		DTypePar(id, pos): id;
		DTypeName(id, typars, pos): id + (if (typars == []) "" else "<" + superglue(typars, \tt -> prettyDType(input, tt), ", ") + ">");
		DTypeFunction(args, returnType, pos): "(" + superglue(args, \tt -> prettyDType(input, tt), ", ") + ") -> " + prettyDType(input, returnType);
	}
}

prettyDStruct(input : string, s : DStruct) -> string {
	s.id + "<" + strGlue(s.typars, ", ") 
	+ ">(" + superglue(s.structArgs, \sa : DStructArg -> {
		(if (sa.ismutable) "mutable " else "") 
		+ sa.id + " : " + prettyDType(input, sa.type)
	}, ", ") + ")";
}
