import plow/dexp/rec;

export {
	// Visit all types in this expression
	foldDTypesInDExp(e : DExp, i : ?, fn : (?, DType) -> ?) -> ?;

	// Find all type-pars in this type
	extractDTypars(acc : Set<string>, d : DType) -> Set<string>;
}

foldDTypesInDExp(e : DExp, acc : ?, fn : (?, DType) -> ?) -> ? {
	switch (e) {
		DLambda(args, body, __): {
			nacc = fold(args, acc, \nacc, a -> {
				fn(nacc, a.type)
			});
			foldDTypesInDExp(body, nacc, fn)
		}
		DSwitch(value, type, cases, defaultCase, pos): {
			acc0 = foldDTypesInDExp(value, fn(acc, type), fn);
			acc1 = fold(cases, acc0, \cacc, case -> {
				foldDTypesInDExp(case.body, cacc, fn)
			});
			defaultCase ?? {
				foldDTypesInDExp(defaultCase, acc1, fn)
			} : acc1;
		}
		DDowncast(value, subtype, scope, pos): {
			nacc = foldDTypesInDExp(value, fn(acc, subtype), fn);
			foldDTypesInDExp(scope, nacc, fn)
		}
		DCast(value, target, pos): {
			foldDTypesInDExp(value, fn(acc, target), fn);
		}
		DTyped(value, subtype, pos): {
			foldDTypesInDExp(value, fn(acc, subtype), fn);
		}
		default: {
			ch = getDExpChildren(e);
			fold(ch, acc, \nacc, ee -> {
				foldDTypesInDExp(ee, nacc, fn)
			})
		}
	}
}

extractDTypars(acc : Set<string>, d : DType) -> Set<string> {
	switch (d) {
		DTypePar(id, pos): insertSet(acc, id);
		DTypeName(id, typars, pos): fold(typars, acc, extractDTypars);
		DTypeFunction(args, returnType, pos): {
			nacc = fold(args, acc, extractDTypars);
			extractDTypars(nacc, returnType);
		}
	}
}
