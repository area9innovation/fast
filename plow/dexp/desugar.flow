import plow/dexp/dexp;
import plow/dexp/desugar_env;
import plow/dexp/unique_id;
import plow/pexp/pexp;
import plow/pexp/values;
import plow/dexp/unions;
import ds/set;
import ds/arrayutils;

/*
Differences to PExp:
- It is not a complete Exp type, but has different unions for toplevel, exp, type
- "Temp" structures such as argtype, case, etc are not in exp
- Union match in switch is expanded
TODO:
- We have constructors resolved to not be calls
- The top level is indexed like a tree
- Introduce downcast in switches
- Expand ?? to switch
- Extract let-bindings from cases
- Collect string includes
- Extract structs and unions
- Extract top-level functions from globals
- Should we have an enum with builtins?
*/

export {
	// Desugar a module
	desugarPExp(dd : DDesugar, flowpath : string, fullpath : string, pexp : PExp) -> DModule;

	getDummyDModule() -> DModule;

	// Extract the imports only
	getDImports(p : PExp) -> [DImport];
}

getDImports(p : PExp) -> [DImport] {
	module = getPModule(p);
	filtermap(module.toplevels, \tl -> {
		switch (tl) {
			PImport(path, pos): Some(DImport(path, pos));
			default: None();
		}
	});
}

getDummyDModule() -> DModule {
	DModule("", "", [], [], DExport(makeSet(), makeSet(), makeSet(), -1), makeTree(), makeTree(), makeTree(), makeTree(), makeTree(), [], -1);
}

desugarPExp(dd : DDesugar, flowpath : string, fullpath : string, pexp : PExp) -> DModule {
	module = getPModule(pexp);
	if (module.pos == -1) dd.onError(pexp.pos, "Expected module");

	imports = getDImports(pexp);
	forbids = filtermap(module.toplevels, \tl -> {
			switch (tl) {
				PForbid(path, pos): {
					if (path == "") dd.onError(pos, "Unknown forbid path");
					Some(DForbid(path, pos));
				}
				default: None();
			}
		});

	// Find all top-levels (including those in the export section)
	toplevels = fold(module.toplevels, module.toplevels, \acc, tl -> {
		switch (tl) {
			PExport(tls, __): concat(acc, tls);
			default: acc;
		}
	});

	// Extract structs & unions in our module, so we can include those in the lookup
	structs = fold(toplevels, makeTree(), \acc, tl -> {
		extractDStruct(dd, acc, tl)
	});
	unions = fold(toplevels, makeTree(), \acc, tl -> {
		extractDUnion(dd, acc, tl)
	});

	// Make sure our lookup knows about local structs and unions
	dd2 = DDesugar(dd with 
		resolveStructName = \name -> {
			mst = lookupTree(structs, name);
			mst ?? Some(mst) : dd.resolveStructName(name);
		},
		resolveUnionName = \name -> {
			mst = lookupTree(unions, name);
			mst ?? Some(mst) : dd.resolveUnionName(name);
		},
	);

	// Figure out what names are exported
	dummyExport = DExport(makeSet(), makeSet(), makeSet(), pexp.pos);
	exports0 = fold(module.toplevels, dummyExport, \acc, tl : PTopLevel -> {
		switch (tl) {
			PExport(tls, p): {
				if (acc != dummyExport) {
					dd.onError(p, "Only one export section is allowed");
				}
				fold(tls, acc, \acc2, etl -> {
					switch (etl) {
						PTypeDef(id, __, __): DExport(acc2 with exportedGlobals = insertSet(acc2.exportedGlobals, id));
						PGlobal(id, __, __): DExport(acc2 with exportedGlobals = insertSet(acc2.exportedGlobals, id));

						PNative(id, isIo, type, name, pos): DExport(acc2 with exportedGlobals = insertSet(acc2.exportedGlobals, id));
						PUnion(typename, types, pos): DExport(acc2 with exportedTypes = insertSet(acc2.exportedTypes, typename.id));
						PStruct(id, structArgs, pos): DExport(acc2 with exportedTypes = insertSet(acc2.exportedTypes, id));

						PStructArg(id, ismutable, type, pos): acc2;
						PModule(__, pos): acc2;
						PImport(path, pos): acc2;
						PForbid(path, pos): acc2;
						PExport(exports, pos): acc2;
					}
				})
			}
			default: acc;
		}
	});

	typedefs = fold(toplevels, makeTree(), \acc, tl : PTopLevel -> {
		switch (tl) {
			PTypeDef(id, type, pos): {
				typedef = DTypeDef(id, ptype2dtype(dd2, type), pos);
				setTree(acc, id, typedef);
			}
			default: acc;
		}
	});

	natives = fold(toplevels, makeTree(), \acc, tl : PTopLevel -> {
		switch (tl) {
			PNative(id, isIo, type, name, pos): {
				nativ = DNative(id, isIo, ptype2dtype(dd, type), name, pos);
				setTree(acc, id, nativ);
			}
			default: acc;
		}
	});

	globalsOrder = fold2(toplevels, makeList(), makeTree(), \ord, acc, tl : PTopLevel -> {
		switch (tl) {
			PGlobal(id, value, pos): {
				global = DGlobal(id, pexp2dexp(dd2, value), pos);
				Pair(Cons(id, ord), setTree(acc, id, global));
			}
			default: Pair(ord, acc);
		}
	});

	// TODO: Traverse the typenames mentioned in the exported names and put those
	// in exportedTypeNames if they are not in exportedTypes
	exports = exports0;

	DModule(
		flowpath,
		fullpath,
		imports,
		forbids,
		exports,
		structs,
		unions,
		typedefs,
		natives,
		globalsOrder.second,
		list2array(globalsOrder.first),
		pexp.pos,
	);
}

ptoplevel2DToplevel(dd : DDesugar, tl : PTopLevel) -> DTopLevel {
	error = \ -> {
		dd.onError(tl.pos, "Expected global, native, function, union, struct or type def");
		DStruct("", [], tl.pos);
	}
	switch (tl) {
		PModule(toplevels, pos): error();
		PImport(path, pos): error();
		PForbid(path, pos): error();
		PExport(exports, pos): error();
		PGlobal(id, value, pos): DGlobal(id, pexp2dexp(dd, value), pos);
		PNative(id, isIo, type, name, pos): DNative(id, isIo, ptype2dtype(dd, type), name, pos);
		PUnion(typename, types, pos): DUnion(ptypename2dtypename(dd, typename), map(types, \t -> ptypename2dtypename(dd, t)), pos);
		PStruct(id, structArgs, pos): DStruct(id, map(structArgs, \sa -> pstructArg2dstructArg(dd, sa)), pos);
		PStructArg(id, ismutable, type, pos): error();
		PTypeDef(id, type, pos): DTypeDef(id, ptype2dtype(dd, type), pos);
	}
}

extractDStruct(dd : DDesugar, acc : Tree<string, DStruct>, tl : PTopLevel) -> Tree<string, DStruct> {
	switch (tl) {
		PStruct(id, structArgs, pos): {
			dstruct = DStruct(id, map(structArgs, \sa -> pstructArg2dstructArg(dd, sa)), pos);
			setTree(acc, id, dstruct);
		}
		PExport(exports, __): fold(exports, acc, \acc2, ex : PTopLevel -> extractDStruct(dd, acc2, ex));
		default: acc;
	}
}

extractDUnion(dd : DDesugar, acc : Tree<string, DUnion>, tl : PTopLevel) -> Tree<string, DUnion> {
	switch (tl) {
		PUnion(typename, types, pos): {
			dunion = DUnion(ptypename2dtypename(dd, typename), map(types, \t -> ptypename2dtypename(dd, t)), pos);
			setTree(acc, typename.id, dunion);
		}
		PExport(exports, __): fold(exports, acc, \acc2, ex : PTopLevel -> extractDUnion(dd, acc2, ex));
		default: acc;
	}
}

pexp2dexp(dd : DDesugar, pexp : PExp) -> DExp {
	error = \ -> {
		dd.onError(pexp.pos, "Expected expression, got " + toString(pexp));
		DVoid(pexp.pos);
	}
	rec = \ee -> pexp2dexp(dd, ee);
	switch (pexp) {
		PVoid(pos): DVoid(pos);
		PBool(v, pos): DBool(v, pos);
		PInt(v, pos): DInt(v, pos);
		PDouble(v, pos): DDouble(v, pos);
		PString(v, pos): DString(v, pos);
		PVar(var, pos): DVar(var, pos);
		PCallOp(op, args, pos): DCallOp(op, map(args, rec), pos);
		PStringInclude(path, pos): DStringInclude(path, pos);
		PLet(var, value, body, pos): DLet(var, rec(value), rec(body), pos);
		PIf(cond, then, else_, pos): DIf(rec(cond), rec(then), rec(else_), pos);
		PCall(fn, args, pos): {
			// Check if this is a struct constructor
			switch (fn) {
				PVar(id, __): {
					mst = dd.resolveStructName(id);
					mst ?? {
						DConstruct(mst.id, map(args, rec), pos);
					} : DCall(rec(fn), map(args, rec), pos);
				}
				default: DCall(rec(fn), map(args, rec), pos);
			}
		}
		PLambda(args, body, pos): DLambda(map(args, \a -> {
			DArgType(a.id, ptype2dtype(dd, a.type), a.pos)
		}), rec(body), pos);
		PRef(value, pos): DRef(rec(value), pos);
		PField(value, field, pos): DField(rec(value), field, pos);
		PSetMutable(lhs, field, value, pos): DSetMutable(rec(lhs), field, rec(value), pos);
		PArray(exps, pos): DArray(map(exps, rec), pos);
		PSequence(exps, pos): {
			dexps = map(exps, rec);
			if (exps == []) DVoid(pos)
			else if (length(dexps) == 1) dexps[0]
			else DSequence(dexps, pos);
		}
		PSwitch(value, cases, pos): desugarPSwitch(dd, pexp);
		PCast(value, target, pos): DCast(rec(value), ptype2dtype(dd, target), pos);
		PTyped(value, type, pos): DTyped(rec(value), ptype2dtype(dd, type), pos);
		PWith(struct, value, fields, pos): desugarPWith(dd, pexp);
		PMaybe(value, match, otherwise, pos): desugarPMaybe(dd, pexp);

		PModule(toplevels, pos): error();
		PImport(path, pos): error();
		PForbid(path, pos): error();
		PExport(exports, pos): error();
		PGlobal(id, value, pos): error();
		PNative(id, isIo, type, name, pos): error();
		PUnion(typename, types, pos): error();
		PStruct(id, structArgs, pos): error();
		PStructArg(id, ismutable, type, pos): error();
		PTypeDef(id, type, pos): error();
		PTypePar(id, pos): error();
		PTypeName(id, typars, pos): error();
		PTypeFunction(args, returnType, pos): error();
		PArgType(id, type, pos): error();

		PList(l, pos): error();
		PFieldAssign(id, value, pos): error();
		PCase(id, args, body, pos): error();
	}
}

ptype2dtype(dd : DDesugar, p : PType) -> DType {
	switch (p) {
		PTypePar(id, pos): DTypePar(id, pos);
		PTypeName(id, typars, pos): ptypename2dtypename(dd, p);
		PTypeFunction(args, returnType, pos): DTypeFunction(map(args, \at -> ptype2dtype(dd, at)), ptype2dtype(dd, returnType), pos);
		PArgType(id, type, pos): ptype2dtype(dd, type); // We just drop the name
	}
}

ptypename2dtypename(dd : DDesugar, p : PTypeName) -> DTypeName {
	DTypeName(p.id, map(p.typars, \tp -> ptype2dtype(dd, tp)), p.pos);
}

pstructArg2dstructArg(dd : DDesugar, s : PStructArg) -> DStructArg {
	DStructArg(s.id, s.ismutable, ptype2dtype(dd, s.type), s.pos)
}

desugarPSwitch(dd : DDesugar, s : PSwitch) -> DExp {
	wrap = \ -> {
		name = makeTempDName(dd);
		pos = s.value.pos;
		DLet(
			name, pexp2dexp(dd, s.value),
			desugarDSwitchWithVar(dd, s, DVar(name, pos), DTypeName("auto", [], pos)),
			pos
		);
	};
	switch (s.value) {
		PVar(n, pos): {
			desugarDSwitchWithVar(dd, s, DVar(n,pos), DTypeName("auto", [], pos));
		}
		PTyped(value, subtype, pos): {
			stype = ptype2dtype(dd, subtype);
			switch (value) {
				PVar(n, pos2): {	
					desugarDSwitchWithVar(dd, s, DVar(n, pos2), stype);
				}
				default: {
					// We know the type, so OK
					name = makeTempDName(dd);
					DLet(
						name, pexp2dexp(dd, s.value),
						desugarDSwitchWithVar(dd, s, DVar(name, pos), stype),
						pos
					);
				}
			}
		}
		default: {
			wrap();
		}
	};
}

// OK, once we know we have a var for the switch, this is how we go
desugarDSwitchWithVar(dd : DDesugar, s : PSwitch, switchvar : DVar, type : DType) -> DSwitch {

	defaults = filter(s.cases, \c : PCase -> c.id == "default");
	def : Maybe<DExp> = if (length(defaults) == 1) Some(pexp2dexp(dd, defaults[0].body))
		else if (defaults == []) None()
		else {
			dd.onError(defaults[0].pos, "Only one default allowed in switch");
			None();
		};

	cases : [DCase] = filtermap(s.cases, \c : PCase -> {
		if (c.id == "default") None()
		else {
			body0 = pexp2dexp(dd, c.body);
			mstruct = dd.resolveStructName(c.id);
			body = switch (mstruct) {
				None(): {
					// It might be a union
					if (c.args != []) {
						dd.onError(c.pos, c.id + " does not accept arguments in case");
					}
					body0;
				}
				Some(struct): {
					if (length(c.args) == length(struct.structArgs)) {
						foldi(c.args, body0, \i, acc, arg : string -> {
							if (arg == "__") {
								acc
							} else {
								// TODO: Check if "arg" appears in body0. If not, no need to bind
								structArg = struct.structArgs[i];
								DLet(arg, DField(switchvar, structArg.id, c.pos), acc, c.pos)
							}
						})
					} else {
						dd.onError(c.pos, c.id + " expects " + i2s(length(struct.structArgs)) + " args, got " + i2s(length(c.args)));
						body0;
					}
				}
			}
			Some(DCase(c.id, body, c.pos));
		}
	});

	// Collect the struct names we have explicitly named
	structs : Set<string> = fold(cases, makeSet(), \acc, case -> {
		mstruct = dd.resolveStructName(case.id);
		mstruct ?? {
			if (containsSet(acc, case.id)) {
				dd.onError(case.pos, "Duplicate case " + case.id);
			}
			insertSet(acc, case.id)
		} : acc;
	});

	// Expand union cases
	unions = concatA(map(cases, \c : DCase -> {
		if (containsSet(structs, c.id)) {
			// Already a struct, keep it as is
			[c]
		} else {
			// A union - expand to the cases we do not already have
			casestructs = expandDTypename2structs(dd, c.id);
			newStructs = filter(casestructs, \ss : DStruct -> {
				!containsSet(structs, ss.id)
			});
			if (newStructs == []) {
				dd.onError(c.pos, "Union case " + c.id + " is superfluous");
			}

			map(newStructs, \str -> {
				DCase(str.id, c.body, c.pos)
			})
		}
	}));

	DSwitch(switchvar, type, unions, def, s.pos)
}

desugarPWith(dd : DDesugar, w : PWith) -> DWith {
	mstruct = dd.resolveStructName(w.struct);
	mstruct ?? {
		DWith(w.struct, pexp2dexp(dd, w.value), 
			map(w.fields, \f : PFieldAssign -> {
				// TODO: Check that this field exists in this struct
				DFieldAssign(f.id, pexp2dexp(dd, f.value), f.pos)
			}), w.pos)
	} : {
		dd.onError(w.pos, "Unknown struct for with: " + w.struct);
		DWith(w.struct, pexp2dexp(dd, w.value), [], w.pos)
	}
}

desugarPMaybe(dd : DDesugar, m : PMaybe) -> DExp {
	// PMaybe(value, match, otherwise, pos);
	println("TODO: Expand maybe");
	pexp2dexp(dd, m.otherwise);
}
	
