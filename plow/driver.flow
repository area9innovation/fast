import plow/dexp/dexp;
import plow/dexp/desugar;
import plow/pexp/parse;

import fast/fast_path;

import sys/concurrent;

export {
	compilePlow(cache : PlowCache, file : string) -> Maybe<DModule>;

	PlowCache(
		// How to report an error
		onError : (string) -> void,
		// The includes we should use to find files
		includes : [string], 
		// A concurrent hash map from flowpath to DMondule of the Modules we have already parsed and desugared
		modules : native,
	);
}

compilePlow(cache : PlowCache, file : string) -> Maybe<DModule> {
	path = fastPath2path(cache.includes, changeFileExt(file, ".flow"));
	flowpath = path2fastPath(cache.includes, path);

	if (containsConcurrentHashMap(cache.modules, flowpath)) {
		Some(getConcurrentHashMap(cache.modules, flowpath, getDummyDModule()));
	} else {
		// OK, we have to parse this
		code = getFileContent(path);

		p = parsePExp(code, \e -> cache.onError(path + ":" + e));
		println("Parsed " + file + " as " + path);

		// OK, parse all the dependent files
		imports = getDImports(p);
		mods = concurrent(false, map(imports, \i -> {
			\ -> compilePlow(cache, i.path)
		}));
		println("Finished parsing imports for " + path);

		dd = DDesugar(flowpath, \n -> {
			// OK, scan our imports for this struct
			cache.onError(path + ": TODO: Find this struct " + n);
			None()
		}, \n -> {
			cache.onError(path + ": TODO: Find this union " + n);
			None()
		}, makeOnError(path, cache.onError));

		dmod = desugarPExp(dd, p);

		setConcurrentHashMap(cache.modules, flowpath, dmod);
		Some(dmod);
	}
}

makeOnError(file : string, onError : (string) -> void) -> (int, string) -> void {
	resolver = ref None();
	\pos, error -> {
		res = onlyOnce(resolver, \ -> {
			text = getFileContent(file);
			makeLineResolver(text);
		});
		linecol = findLine(res, pos); // maxi
		onError(
			file + ":" + i2s(linecol.lineno) + ":" + i2s(linecol.column) + ": " + error
			+ "\n" + linecol.line
			+ "\n" + lpad("^", " ", linecol.lineIndex - 1)
		);
	}
}