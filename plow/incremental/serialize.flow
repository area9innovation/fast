import plow/bexp/bmodule;
import fs/filesystem;
import fast/fast_path;
import text/serialize;

export {
	readBModule(fullpath : string, flowpath : string) -> Maybe<BModule>;
	writeBModule(bmod : BModule) -> void;
	deleteBModule(flowpath : string) -> void;
}

bincrementalVersion = 1;

BIncremental(
	version : int,
	fullpath : string,
	modifiedTimestamp : double,
	checksum : string,
	fileSize : double,
	serialized : string // A serialized bmodule
);

readBModule(fullpath : string, flowpath : string) -> Maybe<BModule> {
	path = getBIncrementalPath(flowpath);
	if (isUrlParameterFalse("incremental")) {
		deleteFile(path);
		None();
	} else if (fileExists(path)) {
		finc = getFileContent(path);
		flinc = deserialize(finc, IllegalStruct());
		if (flinc == IllegalStruct()) {
			None();
		} else {
			inc : BIncremental = flinc;
			if (inc.version == bincrementalVersion) {
				binc = makeBIncremental(fullpath, "");
				if (isBIncrementalSame(inc, binc)) {
					bmod = deserialize(inc.serialized, IllegalStruct());
					if (bmod != IllegalStruct()) {
						// println("Reusing incremental " + flowpath);
						Some(bmod)
					} else {
						// Something wrong with the incremental file. Remove it
						deleteFile(path);
						None();
					}
				} else {
					// OK, the cache is obsolete. Remove it
					deleteFile(path);
					None();
				}
			} else {
				deleteFile(path);
				None();
			}
		}
	} else {
		None();
	}
}

writeBModule(bmod : BModule) -> void {
	path = getBIncrementalPath(bmod.flowpath);
	ser = toString(bmod);
	binc = makeBIncremental(bmod.fullpath, ser);
	ensureDirectoryExists(dirName(path));
	if (setFileContent(path, serialize(binc))) {
		 println("Saved incremental for "+ bmod.flowpath);
	} else {
		 println("Could not save incremental for "+ bmod.flowpath);
	}
}

// Get the file timestamp and md5 of the file content
makeBIncremental(fullpath : string, ser : string) -> BIncremental {
	// println(fullpath);
	BIncremental(
		bincrementalVersion,
		fullpath,
		fileModified(fullpath),
		fileChecksum(fullpath),
		fileSize(fullpath),
		ser,
	);
}

isBIncrementalSame(a : BIncremental, b : BIncremental) -> bool {
	a.fullpath == b.fullpath
	&& a.modifiedTimestamp == b.modifiedTimestamp
	&& a.checksum == b.checksum
	&& a.fileSize == b.fileSize
}

getBIncrementalPath(flowpath : string) -> string {
	changeFileExt(pathCombine(pathCombine(getFlowDir(), "objp"), flowpath), ".objp");
}	

deleteBModule(flowpath : string) -> void {
	path = getBIncrementalPath(flowpath);
	deleteFile(path);
	{}
}
