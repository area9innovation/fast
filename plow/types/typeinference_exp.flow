import plow/types/builtin;
import plow/dexp/pretty;
import plow/types/polymorphism;

export {
	ttypeInferenceDExp(env : TTypeEnv, id : string, d : DExp) -> TTypeEClass;
}

inferenceIndent : ref string = ref "  ";

ttypeInferenceDExp(env : TTypeEnv, topid : string, d : DExp) -> TTypeEClass {
	if (true) {
		doTTtypeInferenceDExp(env, topid, d);
	} else {
		indent = ^inferenceIndent;
		if (true) {
			locals = getTreeKeys(env.localTypes);
			println(indent + "Locals " + toString(locals) + " for inference of " + summarizeDExp(env, d));
			// prettyDExp(env, "", indent, d);
		}

		inferenceIndent := indent + "  ";
		t = doTTtypeInferenceDExp(env, topid, d);
		inferenceIndent := indent;

		println(indent + "gave " + prettyTType(t));
		t;
	}
}

doTTtypeInferenceDExp(env : TTypeEnv, topid : string, d : DExp) -> TTypeEClass {
	restype = \reason, tv, type : TTypeEClass -> {
		TTypeEClass(joinTNodes(env.tmap, type.eclass, tv));
	};
	asEClass = \reason, type -> {
		eclass = findOrMakeTNodeClass(env.tmap, type, reason, [d.pos]);
		TTypeEClass(eclass);
	}

	switch (d) {
		DVoid(pos): asEClass("void", TTypeName("void", []));
		DBool(v, pos): asEClass("bool", TTypeName("bool", []));
		DInt(v, pos): asEClass("int", TTypeName("int", [])); 
		DDouble(v, pos): asEClass("double", TTypeName("double", []));
		DString(v, pos): asEClass("string", TTypeName("string", []));
		DStringInclude(path, pos): asEClass("stringinclude", TTypeName("string", []));
		DVar(id, pos, eclass): {
			mtype = lookupTree(env.localTypes, id);
			type : TTypeEClass = mtype ?? {
				// Local types should NOT be instantiated
				mtype
			} : {
				moptype = getBuiltinTType(id);
				externalType = switch (moptype) {
					None(): {
						modtype : Maybe<TType> = lookupTree(env.moduleTypes, id);
						switch (modtype) {
							None(): env.resolveId(pos, id);
							Some(mdtype): {
								mdtype;
							}
						}
					}
					Some(optype): optype;
				};
				if (isTracingId(env.tracing, StageTypeInference(), 2, topid)) {
					println(id + " : " + prettyTType(externalType));
				}

				// Any other type needs instantiation
				itype = instantiateTTypePar(env, externalType);
				asEClass("var " + id, itype);
			};

			if (isTracingId(env.tracing, StageTypeInference(), 2, topid)) {
				println(id + " : " + prettyTType(type));
			}
			
			restype("var " + id, eclass, type);
		}
		DTyped(value, type, pos, eclass): {
			valtype = ttypeInferenceDExp(env, topid, value);
			ttype = TTypeEClass(dtype2eclass(env.tmap, makeTree(), type));
			unifyTNodes(env.tmap, if (false) valtype else makeTTypeSupertype(env, [valtype], false, pos), ttype, "typed", [pos]);
			restype("typed", eclass, ttype);
		}
		DLet(id, value, body, pos, eclass): {
			tvalue = ttypeInferenceDExp(env, topid, value);
			nenv = TTypeEnv(env with localTypes = setTree(env.localTypes, id, tvalue));
			tbody = ttypeInferenceDExp(nenv, topid, body);
			restype("let " +id, eclass, tbody);
		}
		DIf(cond, then, else_, pos, eclass): {
			tcond = ttypeInferenceDExp(env, topid, cond);
			unifyTNodes(env.tmap, tcond, TTypeName("bool", []), "if-cond", [pos]);

			tthen = ttypeInferenceDExp(env, topid, then);
			telse = ttypeInferenceDExp(env, topid, else_);
			type = makeTTypeSupertype(env, [tthen, telse], false, pos);
			restype("if-body", eclass, type);
		}
		DCall(fn, args, pos, eclass): {
			fntype1 = ttypeInferenceDExp(env, topid, fn);
			// println(summarizeDExp(env, fn) + " : " + prettyTType(fntype1));
			argtypes = map(args, \a -> makeTTypeSupertype(env, [ttypeInferenceDExp(env, topid, a)], false, pos));
			rettype = TTypeEClass(env.mkEClass());
			fntype2 = TTypeFunction(argtypes, rettype);
			// println(prettyTType(fntype1) + " vs " + prettyTType(fntype2));
			id = switch (fn) {
				DVar(fnid, __, __): " " + fnid;
				default: "";
			};
			unifyTNodes(env.tmap, fntype1, fntype2, "call" + id, [pos]);

			restype("call-ret", eclass, rettype);
		}
		DConstruct(structid, args, pos, eclass): {
			mstruct = env.resolveStructName(structid);
			mstruct ?? {
				argTypes = map(args, \arg -> {
					ttypeInferenceDExp(env, topid, arg)
				});
				itype = instantiateTTypeNameForStruct(env, mstruct);
				if (length(argTypes) == length(itype.second)) {
					iteri(itype.second, \i, fields : Pair<string, TType> -> {
						unifyTNodes(env.tmap, makeTTypeSupertype(env, [argTypes[i]], false, pos), fields.second, "construct call", [pos]);
					});
				} else {
					env.onError(pos, "Struct " + structid + " expects " + i2s(length(itype.second)) + " arguments, got " + i2s(length(argTypes)));
				}

				restype("construct " + structid, eclass, asEClass("construct " + structid, itype.first));
			} : {
				env.onError(pos, "Unknown struct " + structid);
				TTypeEClass(env.mkEClass());
			}
		}
		DLambda(args, body, pos, eclass): {
			argTypes : [TTypeEClass] = map(args, \arg -> {
				TTypeEClass(dtype2eclass(env.tmap, makeTree(), arg.type));
			});
			nenv = foldi(args, env, \i, acc, arg -> {
				TTypeEnv(acc with localTypes = setTree(acc.localTypes, arg.id, argTypes[i]))
			});
			retType = ttypeInferenceDExp(nenv, topid, body);
			// TODO: With this as true, we have a problem that super(fn-type) does not preserve
			// the supertype of the return value
			type = TTypeFunction(argTypes, if (false) retType else makeTTypeSupertype(env, [retType], false, pos));
			// println(prettyTType(type) + " for " + strGlue(map(args, \a -> a.id), ","));
			restype("lambda", eclass, asEClass("lambda", type));
		}
		DRef(value, pos, eclass): {
			vtype = ttypeInferenceDExp(env, topid, value);
			type = TTypeName("ref", [makeTTypeSupertype(env, [vtype], false, pos)]);
			restype("ref", eclass, asEClass("ref", type));
		}
		DField(value, field, pos, eclass): {
			// OK, this is an overload of all structs with a field
			tvalue = ttypeInferenceDExp(env, topid, value);

			fieldStructs : [DStruct] = env.resolveField(field);

			// Types for overload functions
			fieldFnTypes : [TTypeFunction] = filtermap(fieldStructs, \struct : DStruct -> {
				getDStructFieldType(env, struct, field);
			});

			fieldUnions : Set<string> = env.resolveUnionField(field);
			unionFns = map(set2array(fieldUnions), \un -> {
				utype = instantiateTTypeName(env, un);

				munion = env.resolveUnionName(un);
				fieldType = switch (munion) {
					None(): TTypeEClass(env.mkEClass());
					Some(union): {
						// Find the type of this field
						fieldType : Maybe<TTypeFunction> = fold(union.types, None(), \acc, type -> {
							mstruct = env.resolveStructName(type.id);
							mstruct ?? {
								getDStructFieldType(env, mstruct, field)
							} : acc;
						});
						switch (fieldType) {
							None(): TTypeEClass(env.mkEClass());
							Some(ft): {
								ft.returnType
							}
						}
					}
				};
				TTypeFunction([utype], fieldType);
			});

			fnTypes = concat(fieldFnTypes, unionFns);

			fntype = if (fnTypes == []) {
				env.onError(pos, "Unknown field " + field);
				TTypeEClass(env.mkEClass());
			} else if (length(fnTypes) == 1) {
				fnTypes[0]
			} else TTypeOverload(TTypeEClass(eclass), sort(fnTypes));

			rtype = TTypeEClass(eclass);
			calltype = TTypeFunction([tvalue], rtype);

			unifyTNodes(env.tmap, calltype, fntype, "field", [pos]);
			rtype;
		}
		DSetMutable(left, field, value, pos, eclass): {
			ltype = ttypeInferenceDExp(env, topid, left);
			valtype = ttypeInferenceDExp(env, topid, value);

			fieldStructs : [DStruct] = env.resolveField(field);

			fieldFnTypes = filtermap(fieldStructs, \struct : DStruct -> {
				stypenameFields = instantiateTTypeNameForStruct(env, struct);
				stypename = stypenameFields.first;
				fields = stypenameFields.second;

				mft : Maybe<Pair<string, TType>> = find(fields, \sa -> sa.first == field);
				fieldType : Maybe<TType> = maybeMap(mft, \ft -> {
					// OK, the typars are wrong here, since we should use the type
					// of the field
					ft.second
				});
				fieldType ?? {
					Some(TTypeFunction([stypename], fieldType));
				} : None();
			});

			fntype = if (fieldFnTypes == []) {
				env.onError(pos, "Unknown field " + field);
				TTypeEClass(env.mkEClass());
			} else if (length(fieldFnTypes) == 1) {
				fieldFnTypes[0]
			} else TTypeOverload(TTypeEClass(eclass), sort(fieldFnTypes));

			calltype = TTypeFunction([ltype], makeTTypeSupertype(env, [valtype], false, pos));

			unifyTNodes(env.tmap, calltype, fntype, "field", [pos]);
			restype("setmutable", eclass, asEClass("setmutable", voidTType))
		}
		DArray(exps, pos, eclass): {
			vtypes = map(exps, \e -> {
				ttypeInferenceDExp(env, topid, e);
			});
			type = if (vtypes == []) {
				// OK, set up a type var for this thing
				TTypeName("array", [TTypeEClass(env.mkEClass())]);
			} else {
				TTypeName("array", [makeTTypeSupertype(env, vtypes, false, pos)]);
			}
			restype("array", eclass, asEClass("array", type));
		}
		DSequence(exps, pos, eclass): {
			stype = fold(exps, voidTType, \acc, e -> {
				ttypeInferenceDExp(env, topid, e);
			});
			restype("seq", eclass, asEClass("seq", stype));
		}
		DSwitch(value, type, cases, defaultCase, pos, eclass): {
			tvalue = ttypeInferenceDExp(env, topid, value);

			ttype = TTypeEClass(dtype2eclass(env.tmap, makeTree(), type));
			unifyTNodes(env.tmap, if (true) tvalue else makeTTypeSupertype(env, [tvalue], false, pos), ttype, "switch val", [pos]);

			// Relate the type of the value with all structs mentioned
			caseTypes = map(cases, \c -> instantiateTTypeName(env, c.id));
			unifyTNodes(env.tmap, makeTTypeSupertype(env, caseTypes, defaultCase == None(), pos), ttype, "switch res", [pos]);

			// Find the resulting type of the bodies
			caseBodyTypes = map(cases, \c -> ttypeInferenceDExp(env, topid, c.body));
			bodytypes = defaultCase ?? {
				arrayPush(caseBodyTypes, ttypeInferenceDExp(env, topid, defaultCase));
			} : {
				caseBodyTypes
			};
			restype("switch ret", eclass, makeTTypeSupertype(env, bodytypes, false, pos));
		}
		DCast(value, target, pos): {
			tvalue = ttypeInferenceDExp(env, topid, value);
			ttarget = TTypeEClass(dtype2eclass(env.tmap, makeTree(), target));
			// We allow casting a type to a supertype
			unifyTNodes(env.tmap, 
				makeTTypeSupertype(env, [tvalue], true, pos), 
				if (true) {
					makeTTypeSupertype(env, [ttarget], true, pos)
				} else {
					// If we do not allow downcasts, use this one
					ttarget
				}, "cast", [pos]
			);
			// TODO: Add an eclass?
			asEClass("cast", ttarget);
		}
		DDowncast(oldid, newid, subtype, __, scope, pos, eclass): {
			moldtype = lookupTree(env.localTypes, oldid);
			moldtype ?? {
				d.supereclass ::= moldtype.eclass;
			} : {
				env.onError(pos, oldid + " in downcast unknown");
			};

			tsubtype = TTypeEClass(dtype2eclass(env.tmap, makeTree(), subtype));
			teclass = asEClass("downcast", tsubtype);

			nenv = TTypeEnv(env with 
				localTypes = setTree(
					env.localTypes,
//					setTree(env.localTypes, oldid, tsubtype),
					newid, teclass
				)
			);
			dtype = ttypeInferenceDExp(nenv, topid, scope);

			// Relate the subtype with the supertype
			etype = TTypeEClass(d.supereclass);
			unifyTNodes(env.tmap, makeTTypeSupertype(env, [teclass], false, pos), etype, "downcast super", [pos]);

			restype("downcast", eclass, dtype);
		}
	}
}

getDStructFieldType(env : TTypeEnv, struct : DStruct, field : string) -> Maybe<TTypeFunction> {
	stypenameFields = instantiateTTypeNameForStruct(env, struct);
	stypename = stypenameFields.first;
	fields = stypenameFields.second;

	mft : Maybe<Pair<string, TType>> = find(fields, \sa -> sa.first == field);
	fieldType : Maybe<TType> = maybeMap(mft, \ft -> {
		// OK, the typars are wrong here, since we should use the type
		// of the field
		ft.second
	});
	fieldType ?? {
		Some(TTypeFunction([stypename], fieldType));
	} : None();
}

makeTTypeSupertype(env : TTypeEnv, subtypes : [TType], maxed : bool, pos : int) -> TTypeEClass {
	ss2 = fold(subtypes, makeSet(), extractTSupers);
	type = TTypeSupertype(env.mkUnique(), set2array(ss2), maxed);
	eclass = makeTNodeClassForType(env.tmap, type, "supertype", [pos]);
	TTypeEClass(eclass);
}

extractTSupers(acc : Set<TType>, t : TType) -> Set<TType> {
	switch (t) {
		TTypeSupertype(__, supers, maxed): fold(supers, acc, extractTSupers);
		default: insertSet(acc, t);
	}
}
