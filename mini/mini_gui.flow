import material/material2tropic;
import material/material_snackbar;

import mini/mini_compiler;
import mini/mini_pretty;

viewMiniDb(m : MiniDb) -> Material {
	MLines(
		foldList(m.stack, [MText("Stack:", [])], \acc, st -> {
			arrayPush(acc, MText(miniPretty(st), []))
		})
	);
}

main() {
	manager = makeMaterialManager([]);
	db0 = MiniDb(
		["."], 
		makeTree(), 
		MiniAst(makeTree(), makeTree(), makeTree(), makeTree(), makeTree()),
		makeList(), 
		\e -> {
			showMSnackbar(manager, e, []);
		}
	);

	db = make(db0);

	file = make("mini/tests/test.mini");

	mrender(manager, true, 
		MLines([
			MText("The Mini Compile Server", [MTitle()]),
			MTextInput(file, [], []),
			MTextButton("COMPILE", \ -> {
				thefile = getValue(file);
				if (thefile != "") {
					next(db, pushMiniCommand(getValue(db), MiniFilename(thefile)));
				}
				next(db, miniCompile(getValue(db)));
			}, [], []),
			MSelect(db, viewMiniDb),
		])
	);
}
