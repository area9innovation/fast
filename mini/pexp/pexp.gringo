flow = '\uFEFF'? ws $$pos toplevels $"module" $"setpos" lastLineComment?;

toplevels = $"list" toplevel $"cons" (toplevel $"cons")* optsemi | $"list";

toplevel = import | export | forbid | native | union | global | functionOrStruct;
	semi = #";" ws;
	optsemi = (";" ws)?;

import = "import" s pathpos ws semi $"import";
export = "export" ws $$pos "{" ws toplevels "}" ws $"export" $"setpos";
forbid = "forbid" s pathpos ws semi $"forbid";
	pathpos = $$pos $path $"setpos";
	path = bid ('/' bid)*;

native = "native" s idpos ':' ws $"list" ("io" ws $$"io" $"cons")? type '=' ws $nativename ws semi $"native";
	nativename = bid ('.' bid)*;

union = typename "::=" ws typenames semi $"union";
	typenames = $"list" typename $"cons" ("," ws typename $"cons")* ("," ws)? | $"list";

global = idpos "=" ws brace optsemi $"global"
		| idpos "=" ws exp semi $"global" 
		| idpos ":" ws type "=" ws brace optsemi $"global_typed"
		| idpos ":" ws type "=" ws exp semi $"global_typed";

functionOrStruct = 
	// Forward declaration
	  idpos fntype brace optsemi $"fndef_typed" 
	| idpos fntype exp semi $"fndef_typed"
	| idpos fntype semi $"typedef"
	| idpos fntypeauto brace optsemi $"fndef_typed" 
	| idpos fntypeauto exp semi $"fndef_typed" 
	| idpos '(' ws structargs ')' ws semi $"structdef" // This is a struct!

	// Forward type declarations
	| idpos ':' ws type semi $"typedef"
	// Struct
	| idpos ':' ws '(' ws structargs ')' ws semi $"structdef"
	;

	// listof(structarg, ",")
	structargs = $"list" structarg $"cons" ("," ws structarg $"cons")* ("," ws)? | $"list";
	structarg = $"list" ("mutable" !letterOrDigit ws $$"mutable" $"cons")? idpos ':' ws type $"structarg";

exp = 
	// These optsemi should really be semi, but we have too much legacy without
	(idpos "=" ws exp optsemi (exp | $"void") $"let"	// The else is error recovery for the common case of missing a body
		| idpos ":" ws type "=" ws exp optsemi exp $"let_typed"
	)
	|> exp $$pos ":=" ws exp $":=" $"setpos"
	|> exp < ( $$pos "|>" ws exp $"|>" $"setpos")*
	|> exp $$pos "||" ws exp $"||" $"setpos"
	|> exp $$pos "&&" ws exp $"&&" $"setpos"
	|> exp $$pos "==" ws exp $"==" $"setpos" | exp $$pos "!=" ws exp $"!=" $"setpos"
	|> exp ($$pos "<=" ws exp $"<=" $"setpos" | $$pos "<" ws exp $"<" $"setpos" | $$pos ">=" ws exp $">=" $"setpos" | $$pos ">" ws exp $">" $"setpos")
	|> exp < ($$pos "+" ws exp $"+" $"setpos" | $$pos "-" ws exp $"-" $"setpos")*
	|> exp ($$pos "*" ws exp $"*" $"setpos" | $$pos "/" ws exp $"/" $"setpos" | $$pos "%" ws exp $"%" $"setpos")*

	// To ensure that ?? does not consider the : as part of the type
	|> exp ($$pos ":" ws type $":" $"setpos")+

	|> exp "??" ws <exp ":" ws exp $"maybe"

	|> (
		$$pos '!' ws exp $"not" $"setpos"
		| $$pos "-" ws exp $"negate" $"setpos"
		| lambda
		| exp (
			$$pos "(" ws exps ")" ws $"call" $"setpos"
			| with
			| $$pos "." ws idpos "::=" ws exp $"::=" $"setpos"
			| $$pos "." ws idpos $"dot" $"setpos"
			| $$pos "[" ws exp "]" ws $"index" $"setpos"
		)*
	)

	|> $$pos '^' ws exp $"deref" $"setpos"

	|> (
		$$pos "if" !letterOrDigit ws "(" ws exp ")" ws exp "else" ws exp $"ifelse" $"setpos"
		| $$pos "if" !letterOrDigit ws "(" ws exp ")" ws exp $"if"  $"setpos"
		| "(" ws exp ")" ws 
		| $$pos "ref" !letterOrDigit ws exp $"ref" $"setpos"
		| switch
		| $$pos "cast" !letterOrDigit ws '(' ws exp "->" ws type ')' ws $"cast" $"setpos"
		| brace
		| $$pos "true" !letterOrDigit ws $"true" $"setpos"
		| $$pos "false" !letterOrDigit ws $"false" $"setpos"
		| $$pos id $"var" $"setpos"
		| $$pos '"#inc' "lude" s pathpos '"' ws $"stringinclude" $"setpos"
		| $$pos string ws $"unescape" $"setpos"
		| $$pos "0x" $(hexdigit+) ws $"hex" $"setpos"
		| $$pos $double ws $"s2d" $"setpos"
		| $$pos $int ws $"s2i" $"setpos"
		| $$pos "[" ws exps "]" ws $"array" $"setpos"
	);

with = $$pos '(' ws exp "with" s fields ')' ws $"with" $"setpos";
	fields = $"list" fieldassign $"cons" ("," ws fieldassign $"cons")*  ("," ws)? | $"list";
		fieldassign = id '=' ws exp $"fieldassign";

switch = "switch" !letterOrDigit ws 
		'(' ws exp ')' ws 
		'{' ws cases #'}' ws $"switch";

	cases = $"list" defaultOrNamedCase $"cons" (defaultOrNamedCase $"cons")*
			| $"list";

	defaultOrNamedCase = 
		"default" !letterOrDigit ws ':' ws exp (';' ws)? $"default" 
		| id '(' ws names ')' ws ':' ws exp (';' ws)?  $"case";

	names = $"list" id $"cons" ("," ws id $"cons")* ("," ws)? | $"list";

brace = $$pos "{" ws expsemi "}" ws $"sequence" $"setpos";
	// The first optsemi here is to have backwards compatibility. Really, it should be semi
	expsemi = $"list" exp $"cons" (optsemi exp $"cons")* optsemi
		| $"list";

lambda = backslash ws lambdaargs "->" ws exp "" $"lambda"; // The "" makes right-recursion disappear
	lambdaargs = $"list" lambdaarg $"cons" ("," ws lambdaarg $"cons")* ("," ws)? 
		| $"list";

	lambdaarg = id ":" ws type $":" |
		idpos $"var" $"auto" $":";	// We add auto automatically

exps = $"list" exp $"cons" ("," ws exp $"cons")* ("," ws)? 
	| $"list";


idpos = $$pos id $"setpos";

//
// Types
//

type = 	
	$$pos "bool" !letterOrDigit ws $"bool" $"setpos"
	| $$pos "int" !letterOrDigit ws $"int" $"setpos"
	| $$pos "double" !letterOrDigit ws $"double" $"setpos"
	| $$pos "string" !letterOrDigit ws $"string" $"setpos"
	| $$pos "flow" !letterOrDigit ws $"flow" $"setpos"
	| $$pos "void" !letterOrDigit ws $"void" $"setpos"
	| $$pos "auto" !letterOrDigit ws $"auto" $"setpos"
	| $$pos "native" !letterOrDigit ws $"nativetype" $"setpos"
	| $$pos "ref" !letterOrDigit ws type $"reftype" $"setpos"
	| fntype
	| typename
	| "[" ws type "]" ws $"arraytype"
	| $$pos $("?"+) ws $"polytype" $"setpos"
	;

fntype = $$pos "(" ws argtypes ")" ws "->" ws type $"fntype" $"setpos";
fntypeauto = $$pos "(" ws argtypes ")" ws $"auto" $"fntype" $"setpos";
	argtypes = $"list" argtype $"cons" ("," ws argtype $"cons")* ("," ws)? | $"list";
	argtype = $$pos id ":" ws type $"argtype" $"setpos" | type;

typename = idpos typars $"typename";
	typars = "<" ws types ">" ws | $"list";

	types = $"list" type $"cons" ("," ws type $"cons")* ("," ws)? 
		| $"list";

//
// Lexing
//

hexdigit = '0'-'9'
	| 'a'-'f'
	| 'A'-'F';

id = $bid ws;

bid = ('a'-'z' | 'A'-'Z' | '_') (letterOrDigit)*;

letterOrDigit = 'a'-'z'
	| 'A'-'Z'
	| '_'
	| '0'-'9';

string = $('"' onechar_* '"') ws;

onechar_ = backslash "u" hexdigit hexdigit hexdigit hexdigit
	| backslash "X" hexdigit hexdigit hexdigit hexdigit
	| backslash "x" hexdigit hexdigit
	| backslash escapedchar_
	| !'"' !backslash anychar;

escapedchar_ = backslash | '"' | "n" | "t" | "r";
backslash = '\';
anychar = '0x0000'-'0xffff';

double = int '.' int? | '.' int;
int = '0'-'9'+;

ws = s*;
s = cs+;

cs = " " | "\t" | "\n" | "//" (!"\n" anychar)* "\n" | "/*" (!"*/" anychar)* "*/";

lastLineComment = "//" (!"\n" anychar)*;

flow
