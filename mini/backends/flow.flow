import mini/backends/bprogram;
import mini/backends/operators;
import text/blueprint;

export {
	bprogram2flow(b : BProgram) -> string;

	bdeclaration2flow(b : BDeclaration) -> string;
}

bprogram2flow(b : BProgram) -> string {
	superglue(b.declarations, bdeclaration2flow, "\n");
}

bdeclaration2flow(b : BDeclaration) -> string {
	switch (b) {
		BFunction(name, args, body, __): {
			blueprint("
				%name%(%args%) {
					%body%
				}", [
				"name", name,
				"args", superglue(args, \fa -> fa.name, ", "),
				"body", bstatement2flow(body)
			]);
		}
		BGlobalVar(name, body, __): name + " = " + bexp2flow(body) + ";";
		BComment(comment): comment;
	}
}

bstatement2flow(b : BStatement) -> string {
	switch (b) {
		BIfStatement(cond, then, else_): {
			blueprint("
				if (%cond%) {
					%then%
				} else {
					%else%
				}", [
				"cond", bexp2flow(cond),
				"then", bstatement2flow(then),
				"else", bstatement2flow(else_),
			])
		}
		BLetStatement(name, value, body): {
			blueprint("
				%name% = %value%;
				%body%", [
					"name", name,
					"value", bexp2flow(value),
					"body", bstatement2flow(body),
			])
		}
		BReturnVoid(): "{}";
		BReturn(val): bexp2flow(val);
		BSequenceStatement(statements): {
			"{" + superglue(statements, bstatement2flow, ";\n") + "}";
		}
		BComment(comment): comment;
	}
}

bexp2flow(b : BExp) -> string {
	switch (b) {
		BValue(): bvalue2flow(b);
		BVar(name, __): name;
		BLet(name, value, body, __): {
			blueprint("
				%name% = %value%;
				%body%", [
					"name", name,
					"value", bexp2flow(value),
					"body", bexp2flow(body),
			])
		}
		BLambda(args, body, __): "\\" + superglue(args, \fa -> fa.name, ", ") + " -> " + bexp2flow(body);
		BCall(fn, args, __): produceCallOrBinOp(b, bexp2flow);
		BIf(cond, then, else_, __): "if (" + bexp2flow(cond) + ") {" + bexp2flow(then) + "} else {" + bexp2flow(else_) + "}";
		BSequence(statements, __): {
			"{" + superglue(statements, bexp2flow, "; ") + "}";
		}
		BComment(comment): comment + "\n";
	}
}

bvalue2flow(b : BValue) -> string {
	switch (b) {
		BInt(value, type): if (type == MiniType2Int(1)) b2s(i2b(value)) else i2s(value);
		BDouble(value): d2s(value);
		BString(value): toString(value);
	}
}
