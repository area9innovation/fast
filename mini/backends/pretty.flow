import mini/backends/bprogram;
import mini/types/pretty;

export {
	prettyBProgram(p : BProgram) -> string;
}

prettyBProgram(p : BProgram) -> string {
	superglue(p.declarations, prettyBDeclaration, "\n");
}

prettyBDeclaration(b : BDeclaration) -> string {
	switch (b) {
		BFunction(name, args, body, tc, type): {
			(if (tc) "tailcall " else "") + name + "(" + superglue(args, \a -> a.name , ", ") + ") : " + miniType2string(type) + " {\n"
			+ prettyBStatement(body)
			+ "\n}"
		}
		BGlobalVar(name, body, type): {
			name + "=" + prettyBExp(body) + ":" + miniType2string(type);
		}
		BNative(name, isio, type, nativeName, mfallback): {
			"native " + name + " : " + (if (isio) "io" else "") + miniType2string(type) + " = " + nativeName
			+ (mfallback ?? (" with " + prettyBDeclaration(mfallback)) : "");
		}
	}
}

prettyBStatement(b : BStatement) -> string {
	switch (b) {
		BIfStatement(cond, then, else_): {
			blueprint("
				if (%cond%) {
					%then%
				} else {
					%else%
				}", [
				"cond", prettyBExp(cond),
				"then", prettyBStatement(then),
				"else", prettyBStatement(else_),
			])
		}
		BLetStatement(name, value, body): {
			blueprint("
				%name% = %value%;
				%body%", [
					"name", name,
					"value", prettyBExp(value),
					"body", prettyBStatement(body),
			])
		}
		BReturnVoid(): "{}";
		BReturn(val): prettyBExp(val);
		BTailCall(fn, args, values, type): "tailcall " + fn + "(" + supergluei(args, \i, arg -> arg + ":" + prettyBExp(values[i]), ", ") + ")";
		BSequenceStatement(statements): {
			"{" + superglue(statements, \s -> prettyBStatement(s), ";\n") + "}";
		}
		BSwitchStatement(val, cases, type): {
			blueprint("
				switch (%val%) {
					%cases%
				} : %type%", [
					"val", prettyBExp(val),
					"cases", superglue(cases, \c -> {
						blueprint("
							%value%: {
								%body%
							}
							",
							["value", prettyBExp(c.value),
							"body", prettyBStatement(c.body)
						])
					}, ""),
					"type", miniType2string(type),
			])
		}
	}
}

prettyBExp(b : BExp) -> string {
	switch (b) {
		BValue(): prettyBValue(b);
		BVar(name, type): name + " : " + miniType2string(type);
		BLet(name, value, body, type): {
			blueprint("
				%name% : %type% = %value%;
				%body%", [
					"name", name,
					"type", miniType2string(type),
					"value", prettyBExp(value),
					"body", prettyBExp(body),
			])
		}
		BLambda(args, body, type): "\\" + superglue(args, \fa -> fa.name, ", ") + " : " + miniType2string(type) + " -> " + prettyBExp(body);
		BCall(fn, args, type): {
			def = \ -> {
				prettyBExp(fn) + "(" + superglue(args, prettyBExp, ", ") + ") : " + miniType2string(type);
			};
			switch (fn) {
				BVar(fname, ftype): {
					if (fname == ";") {
						prettyBExp(args[0]) + ";" + prettyBExp(args[1]) + " : " + miniType2string(type);
					} else {
						fname + "(" + superglue(args, prettyBExp, ", ") + ") : " + miniType2string(type);
					}
				}
				default: def();
			}
		}
		BIf(cond, then, else_, type): {
			"if (" + prettyBExp(cond) + ") {" + prettyBExp(then) + "} else {" + prettyBExp(else_) + "}" + " : " + miniType2string(type);
		}
		BSequence(statements, type): {
			"{" + superglue(statements, \s -> prettyBExp(s), "; ") + "}" + " /* " + miniType2string(type) + "*/";
		}
		BSwitch(val, cases, type): {
			blueprint("
				switch (%val%) {
					%cases%
				} : %type%", [
					"val", prettyBExp(val),
					"cases", superglue(cases, \c -> {
						blueprint("
							%value%: {
								%body%
							}
							",
							["value", prettyBExp(c.value),
							"body", prettyBExp(c.body)
						])
					}, ""),
					"type", miniType2string(type),
			])
		}
	}
}

prettyBValue(b : BValue) -> string {
	switch (b) {
		BInt(value, type): {
			if (type == MiniTypeInt(1)) b2s(i2b(value)) 
			else if (type == MiniTypeInt(0)) "{}"
			else i2s(value);
		}
		BDouble(value): d2s(value);
		BString(value): toString(value);
	}
}
