import mini/types/type;
import maybe;

export {
	// This is the typed AST we send to the backends when they need to compile
	BProgram(declarations : [BDeclaration]);

	// Intermediate representation with code in topological order, but not in statements yet
	BExpProgram(declarations : [BGlobalVar]);

	BDeclaration ::= BGlobalVar, BFunction, BNative;

		BGlobalVar(name : string, body : BExp, type : MiniType);

		BFunction(name : string, args : [BFunArg], body : BStatement, tailCall : bool, type : MiniType);

		BNative(name : string, isio : bool, type : MiniType, nativeName : string, fallback : Maybe<BDeclaration>);

	// Statement-based languages can get this format:
	BStatement ::= BIfStatement, BLetStatement, BReturnVoid, BReturn, BTailCall, BSequenceStatement, BSwitchStatement;
		BIfStatement(cond : BExp, then : BStatement, else_ : BStatement);
		BLetStatement(name : string, value : BExp, body : BStatement);
		BSequenceStatement(statements : [BStatement]);

		// The empty return. We could maybe have BVoid and BVoidType instead?
		BReturnVoid();
		// Return a value
		BReturn(val : BExp);

		// Do a tail call to ourselves with these bindings
		BTailCall(fn : string, args : [string], values : [BExp], type : MiniType);

		BSwitchStatement(expr : BExp, cases : [BCaseStatement], type : MiniType);
			// TODO: We should arguably add a downcast type here
			BCaseStatement(value : BExp, body : BStatement);

	BExp ::= BValue, BVar, BLet, BLambda, BCall, BIf, BSequence, BSwitch;
		BVar(name : string, type : MiniType);
		// The type is the type of the body
		BLet(name : string, value : BExp, body : BExp, type : MiniType);

		BLambda(args : [BFunArg], body : BExp, type : MiniType);
			BFunArg(name : string);

		BCall(fn : BExp, args : [BExp], type : MiniType);
		BIf(cond : BExp, then : BExp, else_ : BExp, type : MiniType);
		BSequence(exps : [BExp], type : MiniType);
		BSwitch(val : BExp, cases : [BCase], type : MiniType);
			// TODO: We should arguably add a downcast type here
			BCase(value : BExp, body : BExp);

	// The backends get this language
	BValue ::= BInt, BDouble, BString;
		BInt(value : int, type : MiniType);
		BDouble(value : double);
		BString(value : string);
}
