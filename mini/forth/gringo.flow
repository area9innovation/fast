import mini/forth/forth;
import mini/forth/stack;
import mini/exp/value;

import gringo/gringo_lib;

import lingo/linecolumn;

export {
	prepareGringoParser(actions : GringoAction<MiniDb>, grammar : string) -> (MiniDb) -> MiniDb;
}

prepareGringoParser(actions : GringoAction<MiniDb>, grammar : string) -> (MiniDb) -> MiniDb {
	gterm = parseGringoGrammar(grammar);

	// println("The grammar we will parse:");
	// println(gterm2string(gterm));

	\db : MiniDb -> {
		textp = popMiniValue(db);
		// Here, we should parse the grammar using Forth actions
		text = getMiniString(textp.first);
		env = GringoEnv(text, makeTree(), 0, actions, textp.second, false, false, makeTree(), 0);
		genv : GringoEnv<MiniDb> = gringoParse(env, gterm);

		if (!isEmptyTree(genv.errors) || genv.i < strlen(env.input) || genv.fail) {
			// TODO: Should we resolve this file name, or push full names?
			file = headList(db.fileStack, "");
			resolver = makeLineResolver(text);

			traverseInOrder(genv.errors, \pos, error -> {
				linecol = findLine(resolver, pos);
				db.onError(file + ":" + i2s(linecol.lineno) + ":" + i2s(linecol.column) + ": " + error);
				db.onError(linecol.line);
				db.onError(lpad("^", " ", linecol.lineIndex - 1));
			})

			if (genv.i < strlen(env.input) || genv.fail) {
				linecol = findLine(resolver, genv.maxi);
				db.onError(file + ":" + i2s(linecol.lineno) + ":" + i2s(linecol.column) + ": Parsing ended here");
			}
		}

		genv.output;
	}
}
