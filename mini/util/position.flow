import mini/exp/exp;
import ds/array;
import net/url_parameter;

export {
	getMiniPos(e : MiniExp) -> int;

	setMiniPos(e : MiniExp, pos : int) -> MiniExp;
}

getMiniPos(e : MiniExp) -> int {
	switch (e) {
		MiniDouble(value, pos): pos;
		MiniString(value, pos): pos;
		MiniInt(value, pos, tyvar): pos;
		MiniVar(name, pos, tyvar): pos;
		MiniLet(name, value, body, pos, tyvar): {
			if (pos == -1 && !isUrlParameterTrue("strict")) getMiniPos(value) else pos;
		}
		MiniLambda(args, body, pos, tyvar): {
			if (pos == -1 && !isUrlParameterTrue("strict")) {
				getMiniPos(body)
			} else pos;
		}
		MiniCall(fn, args, pos, tyvar): {
			if (pos != -1 || isUrlParameterTrue("strict")) {
				pos
			} else {
				cpos = getMiniPos(fn);
				if (cpos != -1) cpos
				else fold(args, -1, \acc, arg -> if (acc != -1) acc else getMiniPos(arg))
			}
		}
	}
}

setMiniPos(e : MiniExp, pos : int) -> MiniExp {
	switch (e) {
		MiniDouble(v, __): MiniDouble(v, pos);
		MiniString(v, __): MiniString(v, pos);
		MiniInt(value, __, tyvar): MiniInt(value, pos, tyvar);
		MiniVar(name, __, tyvar): MiniVar(name, pos, tyvar);
		MiniLet(name, value, body, __, tyvar): MiniLet(name, value, body, pos, tyvar);
		MiniLambda(args, body, __, tyvar): MiniLambda(args, body, pos, tyvar);
		MiniCall(fn, args, __, tyvar): MiniCall(fn, args, pos, tyvar);
	}
}
