import mini/interpreter/interpreter;
import mini/exp/value;
import mini/types/type_ast;
import mini/types/util;
import mini/types/implicit_polymorphism;
import mini/util/position;

export {
	// This does a few desugaring things:
	// - Expand switch and cases to the correct form with variable bindings
	// - "m ?? t : e" is expanded
	// - Make implicit type parameters explicit. Maybe -> Maybe<?>. TODO: Should this be Maybe<auto> instead?
	partialMiniExp(env : MiniInterpreter, e : MiniExp) -> MiniExp;
}

partialMiniExp(env : MiniInterpreter, e : MiniExp) -> MiniExp {
	switch (e) {
		MiniValue(): e;
		MiniVar(name, __, tyvar): e;
		MiniLet(name, value, body, pos, tyvar): {
			pvalue = partialMiniExp(env, value);

			// OK, check if the value is just a name
			isValueId = switch (pvalue) {
				MiniVar(__, __, __): true;
				default: false;
			};

			pe = MiniLet(name, pvalue, partialMiniExp(env, body), pos, tyvar);
			if (name == "__switchvar" && isValueId) {
				// This helps get rid of "__switchvalue" in switches where
				// it is not needed, and that in turn helps downcast work
				// correctly in tests/maybe.flow
				interpretMiniExp(env, pe);
			} else {
				pe;
			}
		}
		MiniLambda(args, body, pos, tyvar): {
			MiniLambda(args, partialMiniExp(env, body), pos, tyvar);
		}
		MiniCall(ofn, oargs, pos, tyvar): {
			fn = partialMiniExp(env, ofn);
			args = map(oargs, \a -> partialMiniExp(env, a));
			def = \ -> {
				MiniCall(fn, args, pos, tyvar);
			};
			switch (fn) {
				MiniVar(fname, __, __): {
					if (fname == "__ctcase") {
						processMiniCase(env, pos, args);
					} else if (fname == "__switch") {
						processMiniSwitch(env, pos, args);
					} else if (fname == "__ctmaybe") {
						processMiniMaybe(env, args);
					} else if (fname == "__compiletime") {
						ct = interpretMiniExp(env, args[0]);
						println(miniPretty(ct));
						ct;
					} else if (fname == "__type") {
						interpretMiniType(env, def());
					} else {
						// if fname is a type, we could evaluate it now if we had some way of representing the result
						def();
					}
				}
				default: def();
			}
		}
	}
}

// This expands switch cases correctly
// __ctcase(id, __cons("a", "b"), body)
processMiniCase(env : MiniInterpreter, pos : int, args : [MiniExp]) -> MiniExp {
	struct : string = getMiniVar(args[0]);
	vars : [string] = map(getMiniArray(args[1]), getMiniString);
	body = partialMiniExp(env, args[2]);

	mstructdef = lookupTree(env.env.structs, struct);
	mstructdef ?? {
		/*
		We turn "Some(v): body;" into the equivalent of this:
		__case(Some, 
			__downcast(__switchvar, __type("Some", <>), 
				// Index
				v = __switchvar."value";
				body;
			)
		)
		*/
		bodyLet = foldi(vars, body, \i, acc, var -> {
						if (var != "__") {
							MiniLet(var, MiniCall(MiniVar(".", pos, 0), [
								MiniVar("__switchvar", pos, 0), 
								MiniString(mstructdef.fields[i].name, pos)
							], pos, 0), acc, pos, 0)
						} else acc
					});

		typars = map(mstructdef.typars, \i -> {
			MiniTypeCall("auto", [])
		});

		MiniCall(MiniVar("__case", pos, 0), 
			[
				MiniCall(
					MiniVar("__caseid", pos, 0),
					[
						MiniVar(struct, pos, 0),
					], pos, 0
				),
				// This implies a downcast in the body of __switchvar
				MiniCall(MiniVar("__downcast", pos, 0), [
					MiniVar("__switchvar", pos, 0),
					miniType2Exp(MiniTypeCall(struct, typars)),
					bodyLet,
				], pos, 0),
			], 
			pos,
			0
		);
	} : {
		// We assume this is a union, which will be expanded by processMiniSwitch
		MiniCall(MiniVar("__case", pos, 0),
			[
				MiniCall(
					MiniVar("__caseid", pos, 0),
					[
						MiniVar(struct, pos, 0),
					], pos, 0
				),
				body
			], pos, 0
		)
	}
}

processMiniSwitch(env : MiniInterpreter, pos : int, args : [MiniExp]) -> MiniExp {
	// The cases
	cases : [MiniExp] = extractMiniCases([], args[1]);

	// __switch(var, cases)

	// Find and expand all unions
	expanded = concatA(map(cases, \c -> {
		switch (c) {
			MiniCall(fn, fargs, __, __): {
				switch (fn) {
					MiniVar(name, vpos, __): {
						if (name == "__defaultcase") [c]
						else {
							// __case(__caseid(__switchvar), __downcast(bindings;body))
							switch (fargs[0]) {
								MiniCall(fn2, args2, __, __): {
									cname = getMiniVar(args2[0]);
									udef = lookupTree(env.env.unions, cname);
									switch (udef) {
										None(): [c];
										Some(def): {
											map(def.typenames, \tn -> {
												sname = getMiniTypeName(tn);
												sbody = fargs[1];
												MiniCall(MiniVar("__case", vpos, 0), [
													MiniCall(MiniVar("__caseid", vpos, 0), [MiniVar(sname, vpos, 0)], -1, 0), sbody
												], vpos, 0)
											});
										}
									}
								}
								default: [c];
							}
						}
					}
					default: [c];
				}
			}
			default: [c];
		}
	}));

	nargs = if (expanded != []) {
		[
			args[0],
			fold(tail(expanded), expanded[0], \acc, c -> {
				MiniCall(MiniVar("__or", pos, 0), [acc, c], pos, 0)
			})
		]
	} else {
		args;
	};

	MiniCall(MiniVar("__switch", pos, 0), nargs, pos, 0)
}

extractMiniCases(acc : [MiniExp], e : MiniExp) -> [MiniExp] {
	switch (e) {
		MiniCall(fn, args, __, __): {
			switch (fn) {
				MiniVar(name, __, __): {
					if (name == "__or") {
						nacc = extractMiniCases(acc, args[0]);
						extractMiniCases(nacc, args[1]);
					} else {
						arrayPush(acc, e);
					}
				}
				default: {
					println("Expected case in switch");
					acc;
				}
			}
		}
		default: {
			println("Expected case in switch");
			acc;
		}
	}
}

processMiniMaybe(env : MiniInterpreter, args : [MiniExp]) -> MiniExp {
	/*
	We expand "a ?? b : c" like this:

	__switchvar = %a%;
	__switch(
		__switchvalue(__switchvar), 
		__or(
			__case(
				__caseid(Some), 
				__downcast(
					__switchvar, 
					__type("Some"), 
					(
						__maybevalue = .(__switchvar, "value");
						replace(%b%, %a%, __maybevalue) 
					)
				)
			), 
			__case(
				__caseid(None), 
				__downcast(
					__switchvar, 
					__type("None"), 
					%c%
				)
			)
		)
	)
	*/

	pos = map(args, getMiniPos);

	MiniLet("__switchvar", args[0], 
		MiniCall(MiniVar("__switch", -1, 0), [
			MiniCall(MiniVar("__switchvalue", -1, 0), [
				MiniVar("__switchvar", -1, 0)
			], -1, 0),
			MiniCall(MiniVar("__or", -1, 0), [
				MiniCall(MiniVar("__case", pos[1], 0), [
					MiniCall(MiniVar("__caseid", -1, 0), [
						MiniVar("Some", -1, 0)
					], -1, 0),
					MiniCall(MiniVar("__downcast", -1, 0), [
						MiniVar("__switchvar", -1, 0),
						MiniCall(MiniVar("__type", -1, 0), [MiniString("Some", -1)], -1, 0),
						MiniLet(
							"__maybeval",
							MiniCall(MiniVar(".", -1, 0), [MiniVar("__switchvar", -1, 0), MiniString("value", -1)], -1, 0),
							// Replace "args[0]" with MiniVar("__maybeval")
							replaceMiniExp(args[1], args[0], MiniVar("__maybeval", -1, 0)),
							-1,
							0
						)
					], -1, 0),
				], -1, 0),
				MiniCall(MiniVar("__case", pos[2], 0), [
					MiniCall(MiniVar("__caseid", -1, 0), [
						MiniVar("None", -1, 0)
					], -1, 0),
					MiniCall(MiniVar("__downcast", -1, 0), [
						MiniVar("__switchvar", -1, 0),
						MiniCall(MiniVar("__type", -1, 0), [MiniString("None", -1)], -1, 0),
						args[2]
					], -1, 0),
				], -1, 0)
			], -1, 0)
		], -1, 0), pos[0], 0
	);
}

// Here, we should ensure that typenames have the correct number of typars
interpretMiniType(e : MiniInterpreter, calltype : MiniExp) -> MiniExp {
	type = convertMiniExp2Type(calltype);
	explicitTypars = resolveImplicitTypePars(e.env, type);
	if (explicitTypars != type) {
		miniType2Exp(explicitTypars);
	} else {
		calltype;
	}
}
