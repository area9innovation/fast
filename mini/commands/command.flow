import mini/exp/exp;
import ds/set;
import algorithms;

export {
	// These are the commands that the underlying compiler understands
	MiniCommand ::= MiniProcessFile, MiniForwardDefine, MiniDefine, MiniTypeInfer;

		// Read a file and if changed, run this Forth command on it. Notice this is async
		MiniProcessFile(usedByFile : string, name : string, asyncCommand : string);

		// Forward define this id to this type from this file
		MiniForwardDefine(file : string, name : string, value : MiniExp);

		// Define this id to this expression from this file
		MiniDefine(file : string, name : string, value : MiniExp);

		// Infer the type of these ids, since they are new
		MiniTypeInfer(ids : Set<string>);

	// What order should commands be run in? 0 is first.
	getMiniCommandPriority(m : MiniCommand) -> int;

	getMiniCommandFromPriority(p : int) -> string;

	// If we need to run all these commands of the same type, can that be optimized?
	combineMiniCommands(m : [MiniCommand]) -> [MiniCommand];

	miniCommand2string(m : MiniCommand) -> string;
}

getMiniCommandPriority(c : MiniCommand) -> int {
	switch (c) {
		MiniProcessFile(__, name, command): 0;
		MiniForwardDefine(file, name, value): 1;
		MiniDefine(file, name, value): 2;
		MiniTypeInfer(ids): 3;
	}
}

getMiniCommandFromPriority(p : int) -> string {
	["Files", "Forwards", "Defines", "Typechecks"][p]
}

miniCommand2string(c : MiniCommand) -> string {
	switch (c) {
		MiniProcessFile(__, name, command): "Process file '" + name + "' with '" + command + "'";
		MiniForwardDefine(file, name, value): "Forward define " + name;
		MiniDefine(file, name, value): "Define " + name;
		MiniTypeInfer(ids): "Infer type " + strGlue(set2array(ids), ", ");
	}
}

combineMiniCommands(ms : [MiniCommand]) -> [MiniCommand] {
	if (ms == []) ms
	else {
		first = ms[0];
		switch (first) {
			MiniProcessFile(__, __, command): uniq(ms);
			MiniForwardDefine(__, __, __): ms;
			MiniDefine(__, __, __): ms;
			MiniTypeInfer(__): {
				[MiniTypeInfer(fold(ms, makeSet(), \acc, tc -> {
					switch (tc) {
						MiniTypeInfer(ss): mergeSets(acc, ss);
						default: acc;
					}
				}))]
			}
		}
	}
}
