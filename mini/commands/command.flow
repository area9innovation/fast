import mini/exp/exp;
import ds/set;
import algorithms;

export {
	// These are the commands that the underlying compiler understands
	MiniCommand ::= MiniEvalFile, MiniDefine, MiniTypeInfer;

		// Read a file and if changed, run this Forth command on it
		MiniEvalFile(name : string, command : string);

		// Define this id to this expression
		MiniDefine(file : string, name : string, value : MiniExp);

		// Infer the type of these ids, since they are new
		MiniTypeInfer(ids : Set<string>);

	// What order should commands be run in? 0 is first.
	getMiniCommandPriority(m : MiniCommand) -> int;
	
	// If we need to run all these commands of the same type, can that be optimized?
	combineMiniCommands(m : [MiniCommand]) -> [MiniCommand];

	miniCommand2string(m : MiniCommand) -> string;
}

getMiniCommandPriority(c : MiniCommand) -> int {
	switch (c) {
		MiniEvalFile(name, command): 0;
		MiniDefine(file, name, value): 1;
		MiniTypeInfer(ids): 2;
	}
}

miniCommand2string(c : MiniCommand) -> string {
	switch (c) {
		MiniEvalFile(name, command): "Eval file '" + name + "' with '" + command + "'";
		MiniDefine(file, name, value): "Define " + name;
		MiniTypeInfer(ids): "Infer type " + strGlue(set2array(ids), ", ");
	}
}

combineMiniCommands(ms : [MiniCommand]) -> [MiniCommand] {
	if (ms == []) ms
	else {
		first = ms[0];
		switch (first) {
			MiniEvalFile(__, command): uniq(ms);
			MiniDefine(__, __, __): ms;
			MiniTypeInfer(__): {
				[MiniTypeInfer(fold(ms, makeSet(), \acc, tc -> {
					switch (tc) {
						MiniTypeInfer(ss): mergeSets(acc, ss);
						default: acc;
					}
				}))]
			}
		}
	}
}
