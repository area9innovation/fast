import mini/exp/exp;
import mini/types/type;
import ds/set;
import algorithms;

export {
	// These are the commands that the underlying compiler understands
	MiniCommand ::= MiniProcessFile, MiniSetAnnotation, MiniTypeDefine, MiniDefine, MiniPopFile, MiniCompileTime, MiniTypeInfer;

		// Read a file and if changed, run this Forth command on it. Notice this is async
		MiniProcessFile(usedByFile : string, name : string, asyncCommand : string);

		// Set an annotation for a given scope (id). If scope is "", then it is all ids in the file.
		// Annotations include: "native" for native declarations
		// "export" for the export section (not done yet)
		MiniSetAnnotation(scope : string, annotation : string, value : MiniExp);

		// Define a type. Used for forward declarations
		MiniTypeDefine(file : string, name : string, type : MiniType);

		// Define this id to this expression from this file. This will pick up any annotations
		// so far for this id as well
		MiniDefine(file : string, name : string, value : MiniExp);

		// Finish processing the current file. Processes unfinished native annotations
		MiniPopFile(file : string);

		// Now it is time for compile time eval of these ids - happens when all dependent files are defined
		// This will finish desugaring
		MiniCompileTime(names : Set<string>);

		// Infer the type of these ids, since they are new - happens once all ids are desugared
		MiniTypeInfer(ids : Set<string>);

	// What order should commands be run in? 0 is first.
	getMiniCommandPriority(m : MiniCommand) -> int;

	// For listing, we can also go the opposite direction
	getMiniCommandFromPriority(p : int) -> string;

	// If we need to run all these commands of the same type, can that be optimized?
	combineMiniCommands(m : [MiniCommand]) -> [MiniCommand];

	miniCommand2string(m : MiniCommand) -> string;
}

getMiniCommandPriority(c : MiniCommand) -> int {
	switch (c) {
		MiniProcessFile(__, name, command): 0;
		MiniSetAnnotation(annotation, scope, value): 1;
		MiniTypeDefine(file, name, type): 2;
		MiniDefine(file, name, value): 3;
		MiniPopFile(file): 4;
		MiniCompileTime(name): 5;
		MiniTypeInfer(ids): 6;
	}
}

getMiniCommandFromPriority(p : int) -> string {
	["Files", "Annotations", "Type defs", "Defines", "Finish files", "Compile time", "Typechecks"][p]
}

miniCommand2string(c : MiniCommand) -> string {
	switch (c) {
		MiniProcessFile(__, name, command): "Process file '" + name + "' with '" + command + "'";
		MiniSetAnnotation(annotation, scope, value): "Set annotation " + annotation + " with scope " + scope;
		MiniTypeDefine(file, name, type): "Type define " + name;
		MiniDefine(file, name, value): "Define " + name;
		MiniPopFile(file): "Pop file " + file;
		MiniCompileTime(names): "Compile time " + strGlue(set2array(names), ", ");
		MiniTypeInfer(ids): "Infer type " + strGlue(set2array(ids), ", ");
	}
}

combineMiniCommands(ms : [MiniCommand]) -> [MiniCommand] {
	if (ms == []) ms
	else {
		first = ms[0];
		switch (first) {
			MiniProcessFile(__, __, command): uniq(ms);
			MiniSetAnnotation(__, __, __): ms;
			MiniTypeDefine(__, __, __): ms;
			MiniDefine(__, __, __): ms;
			MiniPopFile(__): ms;
			MiniCompileTime(names): {
				[MiniCompileTime(fold(ms, makeSet(), \acc, tc -> {
					switch (tc) {
						MiniCompileTime(ss): mergeSets(acc, ss);
						default: acc;
					}
				}))]
			}
			MiniTypeInfer(__): {
				[MiniTypeInfer(fold(ms, makeSet(), \acc, tc -> {
					switch (tc) {
						MiniTypeInfer(ss): mergeSets(acc, ss);
						default: acc;
					}
				}))]
			}
		}
	}
}
