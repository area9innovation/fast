import mini/commands/mini_command;
import mini/exp/mini_ast;
import mini/forth/forth;

export {
	// This is the state of our compiler
	MiniDb(
		// The include path we should use
		includes : [string],

		// The prioritized queue of stuff this compiler needs to work on.
		queue : Tree<int, [MiniCommand]>,

		// The program we are working on - is not consistent yet
		program : MiniAst,

		// The forth stack we use in the compiler server
		forth : MiniForth,

		// TODO: Add a set of outputs we are producing

		// TODO: Add a set of names which have errors. Change this interface accordingly:
		onError : (string) -> void,
	);

	pushMiniCommand(db : MiniDb, mini : MiniCommand) -> MiniDb;

	popMiniCommand(db : MiniDb) -> Pair<MiniDb, Maybe<MiniCommand>>;
}

pushMiniCommand(db : MiniDb, mini : MiniCommand) -> MiniDb {
	prio = getMiniCommandPriority(mini);
	MiniDb(db with queue = treePushToArrayUnique(db.queue, prio, mini));
}

popMiniCommand(db : MiniDb) -> Pair<MiniDb, Maybe<MiniCommand>> {
	mp = popmin(db.queue);
	switch (mp) {
		EmptyPopResult(): Pair(db, None());
		PopResult(prio, commands, rest): {
			db1 = MiniDb(db with queue = rest);
			if (commands == []) {
				popMiniCommand(db1)
			} else {
				// See if we can combine some of them
				comb = combineMiniCommands(commands);
				he = comb[0];
				ta = tail(comb);
				db2 = if (ta != []) MiniDb(db1 with queue = setTree(db1.queue, prio, ta)) else db1;
				Pair(db2, Some(he));
			}
		}
	}
}
