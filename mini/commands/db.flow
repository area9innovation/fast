import mini/commands/command;
import mini/commands/filecache;
import mini/exp/ast;
import mini/forth/forth;

export {
	// This is the state of our compiler
	MiniDb(
		// The include path we should use to find files
		includes : [string],

		// The prioritized queue of stuff this compiler needs to work on.
		queue : Tree<int, [MiniCommand]>,

		// Here we track what files we have read, so we can quickly check if they should be
		// read again or not
		fileDependencies : MiniDependent,
		fileCache : MiniFileCache,

		// The program we are working on
		program : MiniAst,

		// The forth stack we use in the compiler server
		forth : MiniForth,

		// Here, we define macros for the Forth - TODO: Replace with natives
		macros : Tree<string, [string]>,
		// And here we define natives
		natives : Tree<string, (MiniDb) -> MiniDb>,

		// TODO: Add a set of outputs we are producing

		// TODO: Add a set of names which have errors. Change this interface accordingly:
		onError : (string) -> void,
	);

	pushMiniCommand(db : MiniDb, mini : MiniCommand) -> MiniDb;

	popMiniCommand(db : MiniDb) -> Pair<MiniDb, Maybe<MiniCommand>>;
}

pushMiniCommand(db : MiniDb, mini : MiniCommand) -> MiniDb {
	prio = getMiniCommandPriority(mini);
	MiniDb(db with queue = treePushToArrayUnique(db.queue, prio, mini));
}

popMiniCommand(db : MiniDb) -> Pair<MiniDb, Maybe<MiniCommand>> {
	mp = popmin(db.queue);
	switch (mp) {
		EmptyPopResult(): Pair(db, None());
		PopResult(prio, commands, rest): {
			db1 = MiniDb(db with queue = rest);
			if (commands == []) {
				popMiniCommand(db1)
			} else {
				// See if we can combine some of them
				comb = combineMiniCommands(commands);
				he = comb[0];
				ta = tail(comb);
				db2 = if (ta != []) MiniDb(db1 with queue = setTree(db1.queue, prio, ta)) else db1;
				Pair(db2, Some(he));
			}
		}
	}
}
