import mini/exp/exp;
import mini/types/typeenv;
import mini/types/pretty;
import mini/types/tyvarinfo;
import text/blueprint;

export {
	miniPrettyTyped(env : MiniTypeEnv, m : MiniExp) -> string;

	miniPrettyTypeVar(env : MiniTypeEnv, tv : int) -> string;
}

miniPrettyTyped(env : MiniTypeEnv, m : MiniExp) -> string {
	type = \tv -> {
		if (tv == 0) "" else " ·" + miniPrettyTypeVar(env, tv) + "· "
	};
	switch (m) {
		MiniInt(value, __, tv): i2s(value) + type(tv);
		MiniDouble(value, __): d2s(value);
		MiniString(value, __): toString(value);
		MiniVar(name, __, tv): name + type(tv);
		MiniLet(name, value, body, __, tv): blueprint("
			%name% = %value%;
				%body%
			%type%",
			[ "name", name,
			  "type", type(tv),
			  "value", miniPrettyTyped(env, value),
			  "body", miniPrettyTyped(env, body)]
		);
		MiniLambda(args, body, __, tv): blueprint("
			\\%args% %type% {
				%body%
			}",
			[ "args", strGlue(args, ", "),
			  "body", miniPrettyTyped(env, body),
			  "type", type(tv),
		   ]
		);
		MiniCall(fn, args, __, tv): {
			def = \sep -> {
				blueprint("
				%fn%(
					%args%
				)%type%", [
					"fn", miniPrettyTyped(env, fn),
					"args", superglue(args, \a -> miniPrettyTyped(env, a), sep),
					"type", type(tv)
				]);
			}
			switch (fn) {
				MiniVar(fname, __, __): {
					if (fname == "__ifte") {
						def(", ");
					} else if (fname == "__ift") {
						def(", ");
					} else if (fname == ";") {
						def(";\n");
					} else if (contains(["__switch", "__or", "__case", "__downcast"], fname)) {
						def(",\n");
					} else {
						def(", ");
					}
				}
				default: def(", ");
			}
		}
	}
}

miniPrettyTypeVar(env : MiniTypeEnv, tv : int) -> string {
	if (tv == 0) {
		""
	} else {
		mtype = lookupTree(^(env.tyvars), tv);
		mtype ?? {
			miniType2string(mtype) + if (true) {
				 "[=α" + i2s(tv) + "]"
			} else {
				""
			}
		} : {
			lower = lookupMultimap(^(env.tyvarLowerBounds), tv);
			upper = lookupMultimap(^(env.tyvarUpperBounds), tv);
			b = \s : Set<MiniType>, sep -> {
				if (isEmptySet(s)) ""
				else superglue(set2array(s), miniType2string, sep)
			};
			"α" + i2s(tv) 
				+ (if (equalSet(lower, upper)) {
					v = b(lower, ",");
					if (v != "") "≔" + v else ""
				} else { "∊{" + b(lower, "|") + "}..{" + b(upper,"&") + "}"});
		}
	}
}
