import mini/exp/dependent;
import ds/simplegraph;

export {
	// Find an order that respects topological order
	topologicalMiniOrder(ast : MiniDependent, ids : Set<string>) -> [string];
}

MiniTopological(
	ast : MiniDependent,
	queue : Set<string>,
	visited : Set<string>,
	graph : SimpleGraph<string, bool>
);

topologicalMiniOrder(ast : MiniDependent, ids : Set<string>) -> [string] {
	// The arrows point from what is used by something to what uses it
	graph = buildMiniGraph(MiniTopological(ast, ids, makeSet(), makeSimpleGraph()));

	// setFileContent("deps.dot", simpleGraph2dot(graph.graph, "", [], \n -> [DotLabel(n)], \e -> []));

	order = topoSortSimpleGraph(graph.graph);
	filter(order, \dep -> {
		containsSet(ids, dep)
	});
}

buildMiniGraph(acc : MiniTopological) -> MiniTopological {
	mp = popMinSet(acc.queue);
	mp ?? {
		id = mp.value;
		// println("Visiting " + id + " with visted " + strGlue(set2array(acc.visited), ", "));
		nacc = MiniTopological(acc with queue = mp.rest);
		if (containsSet(acc.visited, id)) {
			buildMiniGraph(nacc);
		} else {
			deps = lookupMultimap(acc.ast.dependents, id);
			graph = addSimpleNode(acc.graph, id);
			ngraph = foldSet(deps, graph, \acc2 : SimpleGraph<string, bool>, dep : string -> {
				if (id == dep || hasSimpleEdge(acc2, id, dep)) {
					// We do no need self-loops
					acc2
				} else {
					addSimpleEdge(acc2, id, dep, false, 0)
				}
			});
			acc2 = MiniTopological(
				nacc with graph = ngraph,
				queue = mergeSets(nacc.queue, deps),
				visited = insertSet(nacc.visited, id)
			);
			buildMiniGraph(acc2);
		}
	} : acc;
}
