flow = '\uFEFF'? ws importExport* toplevelDeclaration* $"popfile";

importExport = import
	| dynamicImport
	| export
	| forbid;

// TODO: We should set some marker that a new file is started, instead of immediately adding the parse
import = "import" s $path ws ';' ws $$".flow" $"changeFileExt" $"parsefile";

dynamicImport = "require" s $path ws ';' ws; // TODO: AST

// TODO: AST. We could set some marker that the following are exported.
export = "export" ws '{' ws toplevelDeclaration+ '}' ws; 
forbid = "forbid" s $path ws ';' ws; // TODO: AST
	path = bid ('/' bid)*;

toplevelDeclaration = 
	native
	| assign
	| union
	| functionOrStruct
	| vardecl;

native = "native" s id ':' $$"__native" $"var" ws $$"1" $"s2i" $"nil" ("io" ws $$"io" $"cons")? $"isnil" $"-" type $"list2" '=' ws $name $"cons" ws ';' ws $"call" $$"native" $"swap" $"setannotation";
	name = bid ('.' bid)*;

assign = id '=' ws exp ';'? ws $"define";

union = id typars "::=" ws typenames ';' ws $"uniondef";

	// listof(typename, ",")
	typenames = $"nil" typename $"cons" ("," ws typename $"cons")* ("," ws)? | $"nil";

// For now, we ignore the types
// TODO: Extract the type and use a binop to declare the type of the body
functionOrStruct = 
	// Forward declaration - we drop for now
	  id '(' ws namedargs ')' ws returnType ';' ws $"fntype" $"typedef"
	| id '(' ws structargs ')' ws ';' ws $"structdef" // This is a struct!
	| id '(' ws funargs ')' ws returnType $"drop" brace $"lambda" $"define"
	| id '(' ws funargs ')' ws brace $"lambda" $"define"
	| id '(' ws funargs ')' ws exp ';'? ws $"lambda" $"define"

	// Forward type declarations
	| id ':' ws '(' ws namedargs ')' ws returnType ';' ws $"fntype" $"typedef"
	| id ':' ws '(' ws types ')' ws returnType ';' ws $"fntype" $"typedef"
	// Struct
	| id ':' ws '(' ws structargs ')' ws ';' ws $$"structdef"
	;

	// listof(funarg, ",")
	funargs = $"nil" funarg $"cons" ("," ws funarg $"cons")* ("," ws)? | $"nil";
	// This is what we want:
	// funarg = id ':' ws type $$":" $"binop";
	funarg = id (':' ws type $"drop")?;

	// listof(structarg, ",")
	structargs = $"nil" structarg $"cons" ("," ws structarg $"cons")* ("," ws)? | $"nil";
	structarg = $"nil" ("mutable" !letterOrDigit ws $$"mutable" $"cons")? $"isnil" id $"swap" $"list2" ':' ws type $"cons" $$"__structarg" $"var" $"swap" $"call";

	returnType = ws "->" ws type;

vardecl = id ':' ws type '=' ws exp ';'? ws  $"swap" $$":" $"binop" $"define"
	| id ':' ws type ';'? ws $"typedef";

exp = 
	backslash lambdaargs ws "->" ws exp $"lambda"
	|> (
		id "=" ws exp #";" ws semiexps $"let"
	 	| id ":" ws type "=" ws exp ";" ws $"swap" $$":" $"binop" semiexps $"let"
	)
	|> exp ":=" ws exp $$":=" $"binop"
	|> exp "|>" ws exp $$"|>" $"binop"	// TODO: Should we desugar this here?
	|> exp "||" ws exp $$"||" $"binop"
	|> exp "&&" ws exp $$"&&" $"binop"
	|> (
		exp "==" ws exp $$"==" $"binop"
		| exp "!=" ws exp $$"!=" $"binop"
		| exp "<=" ws exp $$"<=" $"binop"
		| exp "<" ws exp $$"<" $"binop"
		| exp ">=" ws exp $$">=" $"binop"
		| exp ">" ws exp $$">" $"binop"
	)
	|> exp < (
			(
			"+" ws exp $$"+" $"binop"
			| "-" ws exp $$"-" $"binop"
			)*
	)
	|> (exp "*" ws exp $$"*" $"binop"
		| exp "/" ws exp $$"/" $"binop"
		| exp "%" ws exp $$"%" $"binop"
	)
	// To ensure that ?? does not consider the : as part of the type
	|> exp (":" ws type $$":" $"binop")+

	|> (
		exp "??" ws <exp $"list2" ":" ws exp $"cons" $$"__maybe" $"var" $"swap" $"call"
	)
	|> (
		'!' ws exp $$"!" $"unop"
		| '-' ws exp $$"__neg" $"unop"
		| exp 
			(
			"(" ws exps ")" ws $"call"
			| '(' ws exp "with" s $"list2" fields $"cons" ')' ws $$"__with" $"var" $"swap" $"call"
			| "." ws id $"list2" "::=" ws exp $"cons" $$"__mutassign" $"swap" $"call"
			| "." ws id $$"." $"binop"
			| "[" ws exp "]" ws $$"__index" $"binop"
			)+
	)
	|> '^' ws exp $$"__deref" $"unop"
	|> (
		"(" ws exp #")" ws 
		| "true" !letterOrDigit ws $"1" $$"bool" $"type0" $$":" $"binop"
		| "false" !letterOrDigit ws $"0" $$"bool" $"type0" $$":" $"binop"
		| "ref" !letterOrDigit ws exp $$"__ref" $"unop"
		| switch
		| "cast" !letterOrDigit ws $$"__cast" $"var" $"nil" '(' ws exp $"cons" "->" ws type $"cons" ')' ws $"call"
		| "if" !letterOrDigit ws "(" ws $"nil" exp $"cons" ")" ws exp $"cons" "else" ws exp $"cons" $$"__ifte" $"var" $"swap" $"call"
		| "if" !letterOrDigit ws "(" ws $"nil" exp $"cons" ")" ws exp $"cons" $$"__ift" $"var" $"swap" $"call"
		| id $"var"
		| "0x" $(hexdigit+) ws $"parsehex"
		| $('0'-'9'+ "." '0'-'9'*) ws $"s2d"
		| $("." '0'-'9'+) ws $"s2d"
		| int ws $"s2i"
		| brace
		| string
		| "[" ws expcommas "]" ws $$"[" $"unop"
		| "[" ws "]" ws $$"__emptyarray" $"var" $"nil" $"call"
	);

	// listof(fieldassign, ",")
	fields = $"nil" fieldassign $"cons" ("," ws fieldassign $"cons")*  ("," ws)? | $"nil";
		fieldassign = id '=' ws exp $$"__withassign" $"binop";

brace = "{" ws exp (#";" ws exp $$";" $"binop")* (";" ws)? #"}" ws 
	| "{" ws "}" ws $$"__void" $"var" $"nil" $"call";

expcommas = exp ("," ws exp $$"," $"binop")* ("," ws)?;

semiexps = exp (";" ws exp $$";" $"binop")*;

switch = "switch" !letterOrDigit ws '(' ws exp $$"__switchvar" $"swap" $$"__switchvar" $"var"  $$"__switchvalue" $"unop" ')' ws '{' ws cases #'}' ws $$"__switch" $"binop" $"let";

cases = defaultOrNamedCase (defaultOrNamedCase $$"__or" $"binop")* | $$"__void" $"var" $"nil" $"call";

defaultOrNamedCase = 
	"default" !letterOrDigit ws ':' ws exp (';' ws)? $$"__defaultcase" $"unop"
	// __ctcase(id, cons(args), body)
	| $"nil" id $"cons" '(' ws names $"cons" ')' ws ':' ws exp (';' ws)?  $"cons" $$"__ctcase" $"var" $"swap" $"call";

names = $"nil" id $"cons" ("," ws id $"cons")* | $"nil";

type = 	
		"bool" !letterOrDigit ws $$"bool" $"type0"
		| "int" !letterOrDigit ws $$"int" $"type0"
		| "double" !letterOrDigit ws $$"double" $"type0"
		| "string" !letterOrDigit ws $$"string" $"type0"
		| "flow" !letterOrDigit ws $$"flow" $"type0"
		| "void" !letterOrDigit ws $$"void" $"type0"
		| "native" !letterOrDigit ws $$"native" $"type0"
		| "ref" !letterOrDigit ws type $$"ref" $"type1"

		| "(" ws namedargs ")" ws "->" ws type $"fntype"
		| "(" ws types ")" ws "->" ws type $"fntype"
		| typename

		| "[" ws type "]" ws $$"array" $"type1"

		| $("?"+) ws $"type0"
	;

typename = id $"var" typars $"call"; 

typars = "<" ws types ">" ws
	| $"nil";

// listof(type, ",")
types = $"nil" type $"cons" ("," ws type $"cons")* ("," ws)? | $"nil";

	// listof(namedargs, ",")
	namedargs = $"nil" namedarg $"cons" ("," ws namedarg $"cons")* ("," ws)? | $"nil";

	// For now, we just drop the id
	namedarg = id $"drop" ':' ws type;

// listof(lambdaarg)
lambdaargs = $"nil" lambdaarg $"cons" ("," ws lambdaarg $"cons")* ("," ws)? | $"nil";

	// We just drop the type for now
	lambdaarg = funarg;

// listof(exp, ",")
exps = $"nil" exp $"cons" ("," ws exp $"cons")* ("," ws)? | $"nil";


string = '"' "#inc" "lude" s path '"' ws   // TODO: AST
	| singlestring
// 	| $"nil" (singlestring $"cons")+ $"concatStrings"
	;

singlestring = '"' $(onechar_)* '"' ws;

onechar_ = backslash "u" hexdigit hexdigit hexdigit hexdigit
	| backslash "X" hexdigit hexdigit hexdigit hexdigit
	| backslash "x" hexdigit hexdigit
	| backslash escapedchar_
	| !'"' !backslash anychar;

hexdigit = '0'-'9'
	| 'a'-'f'
	| 'A'-'F';

escapedchar_ = backslash | '"' | "n" | "t" | "r";

anychar = '0x0000'-'0xffff';

id = $bid ws;

bid = ('a'-'z' | 'A'-'Z' | '_') (letterOrDigit)*;

letterOrDigit = 'a'-'z'
	| 'A'-'Z'
	| '_'
	| '0'-'9';


int = $'0'-'9'+;

backslash = '\';

ws = cs*;

s = cs+;

// We do not have \n and \t and such, so these are here verbatim
cs = " " | "	" | "
" | "//" (!"
" anychar)* "
" | "/*" (!"*/" anychar)* "*/";
flow
