import mini/types/typeenv;
import mini/types/pretty;
import ds/treeutils;

export {
	makeMiniTypeEnv(ast : MiniAst) -> MiniTypeEnv;

	// Do type inference on this declaration
	typeInferenceMiniDeclaration(env : MiniTypeEnv, id : string, ex : MiniExp) -> MiniTypeEnv;
}

makeMiniTypeEnv(ast : MiniAst) -> MiniTypeEnv {
	MiniTypeEnv(
		ast,
		makeTree(),
		ref makeTree(),
		ref makeTree(),
		ref 1,
		ref makeTree(),
	);
}

typeInferenceMiniDeclaration(env : MiniTypeEnv, id : string, ex : MiniExp) -> MiniTypeEnv {
	type = makeMiniType2Var(env);

	nenv = setMiniLocalType2(env, id, type);

	rt = typeInferenceMini(nenv, ex);

	// OK, we constrain the type
	constrainMiniType(nenv, rt, type);

	nenv;
}

/*
We follow the algorithm from here:
https://dl.acm.org/doi/abs/10.1145/3409006

There is an implementation here:
https://github.com/LPTK/simple-sub
*/
typeInferenceMini(env : MiniTypeEnv, ex : MiniExp) -> MiniType2 {
	switch (ex) {
		MiniInt(value, __): setMiniType2Var(env, ex, MiniType2Int(32));
		MiniDouble(value): MiniType2Call("double", []);
		MiniString(value): MiniType2Call("string", []);
		MiniVar(name, __): {
			mlocal = lookupTree(env.localTypes, name);
			setMiniType2Var(env, ex, mlocal ?? mlocal : {
				mglobal = lookupTree(env.ast.types, name);
				switch (mglobal) {
					None(): {
						println("Unknown name " + name);
						makeMiniType2Var(env);
					}
					Some(t): t;
				}
			});
		}
		MiniLet(name, value, body, tv): {
			vtype = typeInferenceMini(env, value);
			nenv = setMiniLocalType2(env, name, vtype);
			setMiniType2Var(nenv, ex, typeInferenceMini(nenv, body));
		}
		MiniLambda(args, body, __): {
			argTypes = map(args, \arg -> makeMiniType2Var(env));
			nenv = foldi(args, env, \i, acc, arg -> {
				setMiniLocalType2(acc, arg, argTypes[i])
			});
			setMiniType2Var(nenv, ex, typeInferenceMini(nenv, body));
		}
		MiniCall(fn, args, __): {
			resType = makeMiniType2Var(env);
			fnType = typeInferenceMini(env, fn);
			callType = MiniType2Function(
				map(args, \a -> typeInferenceMini(env, a)),
				resType
			);
			constrainMiniType(env, fnType, callType);
			setMiniType2Var(env, ex, resType);
		}
		MiniTypeInt(bits): MiniType2Int(bits);
		MiniTypeFunction(args, returnType): {
			argTypes = map(args, \a -> {
				typeInferenceMini(env, a)
			});
			retType = typeInferenceMini(env, returnType);
			MiniType2Function(argTypes, retType);
		}
		MiniTypeCall(id, typars): {
			MiniType2Call(id, map(typars, \tp -> typeInferenceMini(env, tp)));
		}
	}
}

constrainMiniType(env : MiniTypeEnv, lhs : MiniType2, rhs : MiniType2) -> void {
	// if cache contains (lhs, rhs) return else cache += (lhs, rhs)

	// println("Constrain " + mtype2string(lhs) + " <: " + mtype2string(rhs));

	error = \ -> {
		println("Cannot constrain " + mtype2string(lhs) + " <: " + mtype2string(rhs));
	}

	rightTyVar = \rid -> {
		addMiniLowerBound(env, rid, lhs);
		constrainUpperBounds(env, rid, lhs);
	}

	todo = \ -> {
		println("TODO: Implement constrain " + mtype2string(lhs) + " <: " + mtype2string(rhs));
	}

	switch (lhs) {
		MiniType2Int(lbits): {
			switch (rhs) {
				MiniType2Int(rbits): if (lbits != rbits) error();
				MiniType2Var(rid): rightTyVar(rid);
				default: error();
			}
		}
		MiniType2Function(largs, lreturnType): {
			switch (rhs) {
				MiniType2Function(rargs, rreturnType): {
					// (Function(l0, r0), Function(l1, r1)) => constrain(l1, l0); constrain(r0, r1)
					if (length(largs) != length(rargs)) {
						error();
					} else {
						iteri(largs, \i, larg -> {
							constrainMiniType(env, rargs[i], larg);
						});
						constrainMiniType(env, lreturnType, rreturnType);
					}
				}
				MiniType2Var(rid): rightTyVar(rid);
				default: error();
			}
		}

		MiniType2Call(lid, ltypars): {
			switch (rhs) {
				MiniType2Call(rid, rtypars): {
					// TODO: Maybe we should instantiate these types?
					if (lid != rid || length(ltypars) != length(rtypars)) {
						error();
					} else {
						iteri(ltypars, \i, larg -> {
							constrainMiniType(env, rtypars[i], larg);
						});
					}
				}
				MiniType2Var(rid): rightTyVar(rid);
				default: error();
			}
		}
		MiniType2Var(lid): {
			addMiniUpperBound(env, lid, rhs);
			constrainLowerBounds(env, lid, rhs);
		}
		MiniType2Par(lid): {
			switch (rhs) {
				MiniType2Par(rid): {
					// OK, maybe we should instantiate these types
					if (lid != rid) {
						error();
					}
				}
				MiniType2Var(rid): rightTyVar(rid);
				default: error();
			}
		}
		MiniType2Record(fields): {
			/*
			(Record(fs0), Record(fs1)) =>
				fs1.foreach { 
					case (n1, t1) =>
						fs0.find(_._1 == n1) match {
							None => err("missing field: " + n1 + " in " + lhs);
							Some((_, t0)) => constrain(t0, t1) 
						}
				}
			*/
			todo();
		}
	}
}

makeMiniType2Var(env : MiniTypeEnv) -> MiniType2Var {
	id = ^(env.tyvarId);
	env.tyvarId := id + 1;
	MiniType2Var(id);
}

setMiniLocalType2(env : MiniTypeEnv, id : string, type : MiniType2) -> MiniTypeEnv {
// 	println(id + " = " + toString(type));
	MiniTypeEnv(env with localTypes = setTree(env.localTypes, id, type));
}

setMiniType2Var(env : MiniTypeEnv, ex : MiniExp, type : MiniType2) -> MiniType2 {
	def = \tv -> {
		id = if (tv == 0) {
			ttv = makeMiniType2Var(env);
			switch (ex) {
				MiniInt(value, tyvar): ex.tyvar ::= ttv.id;
				MiniVar(name, tyvar): ex.tyvar ::= ttv.id;
				MiniLet(name, value, body, tyvar): ex.tyvar ::= ttv.id;
				MiniLambda(args, body, tyvar): ex.tyvar ::= ttv.id;
				MiniCall(fn, args, tyvar): ex.tyvar ::= ttv.id;

				MiniTypeInt(bits): {};
				MiniTypeFunction(args, returnType): {};
				MiniTypeCall(id, typars): {};
				MiniDouble(value): {};
				MiniString(value): {};
			};
			ttv.id;
		} else tv;
		addMiniLowerBound(env, id, type);
		addMiniUpperBound(env, id, type);
		type;
	}
	switch (ex) {
		MiniInt(value, tyvar): def(tyvar);
		MiniVar(name, tyvar): def(tyvar);
		MiniLet(name, value, body, tyvar): def(tyvar);
		MiniLambda(args, body, tyvar): def(tyvar);
		MiniCall(fn, args, tyvar): def(tyvar);

		MiniTypeInt(bits): type;
		MiniTypeFunction(args, returnType): type;
		MiniTypeCall(id, typars): type;
		MiniDouble(value): type;
		MiniString(value): type;
	}
}

addMiniLowerBound(env : MiniTypeEnv, tyvar : int, type : MiniType2) -> void {
	env.tyvarLowerBounds := insertMultimap(^(env.tyvarLowerBounds), tyvar, type);
}

addMiniUpperBound(env : MiniTypeEnv, tyvar : int, type : MiniType2) -> void {
	env.tyvarUpperBounds := insertMultimap(^(env.tyvarUpperBounds), tyvar, type);
}

constrainLowerBounds(env : MiniTypeEnv, tyvar : int, type : MiniType2) -> void {
	types = lookupMultimap(^(env.tyvarLowerBounds), tyvar);
	iterSet(types, \ltype -> {
		constrainMiniType(env, ltype, type)
	})
}

constrainUpperBounds(env : MiniTypeEnv, tyvar : int, type : MiniType2) -> void {
	types = lookupMultimap(^(env.tyvarUpperBounds), tyvar);
	iterSet(types, \utype -> {
		constrainMiniType(env, type, utype)
	})
}
