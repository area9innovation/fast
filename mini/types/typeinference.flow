import mini/types/typeenv;
import mini/types/pretty;
import mini/types/type_ast;
import mini/types/polymorphism;
import ds/treeutils;

export {
	makeMiniTypeEnv(ast : MiniAst) -> MiniTypeEnv;

	// Do type inference on this declaration. If it is recursive, send in a bool
	typeInferenceMiniDeclaration(env : MiniTypeEnv, id : string, recursive : bool, ex : MiniExp) -> MiniTypeEnv;
}

makeMiniTypeEnv(ast : MiniAst) -> MiniTypeEnv {
	MiniTypeEnv(
		ast,
		makeTree(),
		ref makeTree(),
		ref makeTree(),
		ref 1,
		ref makeTree(),
		ref MiniTypeParMap(makeTree(), makeTree()),
		ref makeSet()
	);
}

typeInferenceMiniDeclaration(env : MiniTypeEnv, id : string, recursive : bool, ex : MiniExp) -> MiniTypeEnv {
	// Reset polymorphism for this id
	initTyparMap(env);

	// TODO: Traverse the body and extract polymorphism and instantiate it from the forward declarations
	mforward = lookupTree(env.ast.types, id);

	if (recursive) {
		// OK, we have to add a local variable of this name and constrain it
		type = makeMiniTypeVar(env);
		nenv = setMiniLocalType(env, id, type);
		rt = typeInferenceMini(nenv, ex);

		// OK, we constrain the type
		constrainMiniType(nenv, rt, type);

		// Back-substitute polymorphism
		ptype = typeVars2TypePars(nenv, rt);

		setMiniTypeDeclaration(nenv, id, ptype);
	} else {
		rt = typeInferenceMini(env, ex);

		// Back-substitute polymorphism
		ptype = typeVars2TypePars(env, rt);

		setMiniTypeDeclaration(env, id, ptype);
	};
}

setMiniTypeDeclaration(env : MiniTypeEnv, id : string, type : MiniType) -> MiniTypeEnv {
	// See if we already have a forward declaration. 
	// If so, that should serve as a constraint.
	mtype = lookupTree(env.ast.types, id);
	mtype ?? {
		constrainMiniType(env, type, mtype);
	} : {}

	MiniTypeEnv(
		env with ast = MiniAst(env.ast with types = setTree(env.ast.types, id, type))
	)
}

/*
We follow the algorithm from here:
https://dl.acm.org/doi/abs/10.1145/3409006

There is an implementation here:
https://github.com/LPTK/simple-sub
*/
typeInferenceMini(env : MiniTypeEnv, ex : MiniExp) -> MiniType {
	switch (ex) {
		MiniInt(value, __): setMiniTypeVar(env, ex, MiniTypeInt(32));
		MiniDouble(value): MiniTypeCall("double", []);
		MiniString(value): MiniTypeCall("string", []);
		MiniVar(name, __): {
			mlocal = lookupTree(env.localTypes, name);

			type = mlocal ?? mlocal : {
				mglobal = lookupTree(env.ast.types, name);
				switch (mglobal) {
					None(): {
						println("Does not know var " + name);
						makeMiniTypeVar(env);
					}
					Some(t): t;
				}
			};

			// OK, we should instantiate this polymorphism
			itype = localTypePars2TypeVars(env, type);

			setMiniTypeVar(env, ex, itype);
		}
		MiniLet(name, value, body, tv): {
			vtype = typeInferenceMini(env, value);
			nenv = setMiniLocalType(env, name, vtype);
			setMiniTypeVar(nenv, ex, typeInferenceMini(nenv, body));
		}
		MiniLambda(args, body, __): {
			argTypes = map(args, \arg -> makeMiniTypeVar(env));
			nenv = foldi(args, env, \i, acc, arg -> {
				setMiniLocalType(acc, arg, argTypes[i])
			});
			retType = typeInferenceMini(nenv, body);
			fnType = MiniTypeFunction(argTypes, retType);
			setMiniTypeVar(nenv, ex, fnType);
		}
		MiniCall(fn, args, __): {
			def = \ -> {
				resType = makeMiniTypeVar(env);
				fnType = typeInferenceMini(env, fn);
				callType = MiniTypeFunction(
					map(args, \a -> typeInferenceMini(env, a)),
					resType
				);
				constrainMiniType(env, fnType, callType);
				setMiniTypeVar(env, ex, resType);
			}
			switch (fn) {
				MiniVar(fname, __): {
					if (fname == "__type") {
						type = convertMiniExp2Type(ex);

						// OK, we have to fix the type of the construct so the AST is still completely types
						// even if the type is non-sense
						set = \e -> {setMiniTypeVar(env, e, miniTypeVoid); {} };
						set(fn);
						iter(args, set);
						// Set the real type;
						setMiniTypeVar(env, ex, type);
					} else def();
				}
				default: def();
			}

		}
	}
}

constrainMiniType(env : MiniTypeEnv, lhs : MiniType, rhs : MiniType) -> void {
	if (containsSet(^(env.constraintCache), Pair(lhs, rhs))) {
		// OK, already handled
	} else {
		env.constraintCache := insertSet(^(env.constraintCache), Pair(lhs, rhs));
		
		// println("Constrain " + mtype2string(lhs) + " <: " + mtype2string(rhs));

		error = \ -> {
			println("Cannot constrain " + mtype2string(lhs) + " <: " + mtype2string(rhs));
		}

		rightTyVar = \rid -> {
			addMiniLowerBound(env, rid, lhs);
			constrainUpperBounds(env, rid, lhs);
		}

		todo = \ -> {
			println("TODO: Implement constrain " + mtype2string(lhs) + " <: " + mtype2string(rhs));
		}

		switch (lhs) {
			MiniTypeInt(lbits): {
				switch (rhs) {
					MiniTypeInt(rbits): if (lbits != rbits) error();
					MiniTypeVar(rid): rightTyVar(rid);
					default: error();
				}
			}
			MiniTypeFunction(largs, lreturnType): {
				switch (rhs) {
					MiniTypeFunction(rargs, rreturnType): {
						// (Function(l0, r0), Function(l1, r1)) => constrain(l1, l0); constrain(r0, r1)
						if (length(largs) != length(rargs)) {
							error();
						} else {
							iteri(largs, \i, larg -> {
								constrainMiniType(env, rargs[i], larg);
							});
							constrainMiniType(env, lreturnType, rreturnType);
						}
					}
					MiniTypeVar(rid): rightTyVar(rid);
					default: error();
				}
			}

			MiniTypeCall(lid, ltypars): {
				switch (rhs) {
					MiniTypeCall(rid, rtypars): {
						if (lid != rid || length(ltypars) != length(rtypars)) {
							error();
						} else {
							iteri(ltypars, \i, larg -> {
								constrainMiniType(env, rtypars[i], larg);
							});
						}
					}
					MiniTypeVar(rid): rightTyVar(rid);
					default: error();
				}
			}
			MiniTypeVar(lid): {
				def = \ -> {
					addMiniUpperBound(env, lid, rhs);
					constrainLowerBounds(env, lid, rhs);
				}

				switch (rhs) {
					MiniTypeVar(rid): if (lid != rid) {
//						rightTyVar(rid);
						def();
					};
					default: def();
				}
			}
			MiniTypePar(lid): {
				switch (rhs) {
					MiniTypePar(rid): {
						if (lid != rid) {
							error();
						}
					}
					MiniTypeVar(rid): rightTyVar(rid);
					default: error();
				}
			}
			MiniTypeRecord(fields): {
				/*
				(Record(fs0), Record(fs1)) =>
					fs1.foreach { 
						case (n1, t1) =>
							fs0.find(_._1 == n1) match {
								None => err("missing field: " + n1 + " in " + lhs);
								Some((_, t0)) => constrain(t0, t1) 
							}
					}
				*/
				todo();
			}
		}
	}
}

setMiniLocalType(env : MiniTypeEnv, id : string, type : MiniType) -> MiniTypeEnv {
// 	println(id + " = " + toString(type));
	MiniTypeEnv(env with localTypes = setTree(env.localTypes, id, type));
}

setMiniTypeVar(env : MiniTypeEnv, ex : MiniExp, type : MiniType) -> MiniType {
	def = \tv -> {
		id = if (tv == 0) {
			ttv = makeMiniTypeVar(env);
			switch (ex) {
				MiniInt(value, tyvar): ex.tyvar ::= ttv.id;
				MiniVar(name, tyvar): ex.tyvar ::= ttv.id;
				MiniLet(name, value, body, tyvar): ex.tyvar ::= ttv.id;
				MiniLambda(args, body, tyvar): ex.tyvar ::= ttv.id;
				MiniCall(fn, args, tyvar): ex.tyvar ::= ttv.id;

				MiniDouble(value): {};
				MiniString(value): {};
			};
			ttv.id;
		} else tv;
		addMiniLowerBound(env, id, type);
		addMiniUpperBound(env, id, type);
		type;
	}
	switch (ex) {
		MiniInt(value, tyvar): def(tyvar);
		MiniVar(name, tyvar): def(tyvar);
		MiniLet(name, value, body, tyvar): def(tyvar);
		MiniLambda(args, body, tyvar): def(tyvar);
		MiniCall(fn, args, tyvar): def(tyvar);

		MiniDouble(value): type;
		MiniString(value): type;
	}
}

addMiniLowerBound(env : MiniTypeEnv, tyvar : int, type : MiniType) -> void {
	env.tyvarLowerBounds := insertMultimap(^(env.tyvarLowerBounds), tyvar, type);
}

addMiniUpperBound(env : MiniTypeEnv, tyvar : int, type : MiniType) -> void {
	env.tyvarUpperBounds := insertMultimap(^(env.tyvarUpperBounds), tyvar, type);
}

constrainLowerBounds(env : MiniTypeEnv, tyvar : int, type : MiniType) -> void {
	types = lookupMultimap(^(env.tyvarLowerBounds), tyvar);
	iterSet(types, \ltype -> {
		constrainMiniType(env, ltype, type)
	})
}

constrainUpperBounds(env : MiniTypeEnv, tyvar : int, type : MiniType) -> void {
	types = lookupMultimap(^(env.tyvarUpperBounds), tyvar);
	iterSet(types, \utype -> {
		constrainMiniType(env, type, utype)
	})
}
