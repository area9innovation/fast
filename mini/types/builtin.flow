import ds/tree;
import ds/set;
import mini/types/type;

export {
	// Some common types
	miniTypeVoid : MiniType;
	miniTypeBool : MiniType;
	miniTypeInt : MiniType;
	miniTypeDouble : MiniType;
	miniTypeString : MiniType;

	getBuiltinMiniTypes() -> Tree<string, MiniType>;

	// What builtin names are pure? cast & ref are NOT
	getBuiltinPureNames() -> Set<string>;
}

miniTypeVoid : MiniType = MiniTypeInt(0);
miniTypeBool : MiniType = MiniTypeInt(1);
miniTypeInt : MiniType = MiniTypeInt(32);
miniTypeDouble : MiniType = MiniTypeCall("double", []);
miniTypeString : MiniType = MiniTypeCall("string", []);

getBuiltinMiniTypes() -> Tree<string, MiniType> {
	v = miniTypeVoid;
	b = miniTypeBool;
	i = MiniTypeInt(32);
	s = miniTypeString;

	at = \t -> MiniTypeCall("array", [t]);
	reftype = \t -> MiniTypeCall("ref", [t]);

	ta = MiniTypePar("?");
	tb = MiniTypePar("??");
	tc = MiniTypePar("???");
	td = MiniTypePar("????");
	te = MiniTypePar("?????");
	tf = MiniTypePar("??????");

	pairs2tree([
		Pair("__ifte", MiniTypeFunction([b, ta, ta], ta)),
		Pair("__ift", MiniTypeFunction([b, v, v], v)),

		Pair(";", MiniTypeFunction([ta, tb], tb)),

		Pair("||", MiniTypeFunction([b, b], b)),
		Pair("&&", MiniTypeFunction([b, b], b)),

		Pair("==", MiniTypeFunction([ta, ta], b)),
		Pair("!=", MiniTypeFunction([ta, ta], b)),
		Pair("<=", MiniTypeFunction([ta, ta], b)),
		Pair("<", MiniTypeFunction([ta, ta], b)),
		Pair(">=", MiniTypeFunction([ta, ta], b)),
		Pair(">", MiniTypeFunction([ta, ta], b)),

		Pair("+", MiniTypeFunction([ta, ta], ta)),
		Pair("-", MiniTypeFunction([ta, ta], ta)),

		Pair("*", MiniTypeFunction([ta, ta], ta)),
		Pair("/", MiniTypeFunction([ta, ta], ta)),
		Pair("%", MiniTypeFunction([ta, ta], ta)),

		Pair("!", MiniTypeFunction([b], b)),
		Pair("__neg", MiniTypeFunction([ta], ta)),

		// : is special and should probably be grabbed by the type inference?
		Pair(":", MiniTypeFunction([ta, tb], tb)),

		// Array constructs
		Pair("__emptyarray", MiniTypeFunction([], at(ta))),
		Pair("[", MiniTypeFunction([ta], at(ta))),
		Pair(",", MiniTypeFunction([ta, ta], ta)),

		Pair("__index", MiniTypeFunction([at(ta), i], ta)),

		// switch
		Pair("__switch", MiniTypeFunction([ta, tb], tb)),
		Pair("__switchvalue", MiniTypeFunction([ta], tb)),
		Pair("__case", MiniTypeFunction([ta, tb], tb)),
		Pair("__defaultcase", MiniTypeFunction([ta], ta)),
			Pair("__or", MiniTypeFunction([ta, ta], ta)),
		Pair("__caseid", MiniTypeFunction([ta], tb)),
		Pair("__casebody", MiniTypeFunction([ta, tb, tc], tc)),

		Pair("__switchvar", ta),

		Pair("__pattern0", MiniTypeFunction([s], ta)),
		// For patterns with arguments
		Pair("__pattern1", MiniTypeFunction([s,ta], tb)),

		Pair("__maybe", MiniTypeFunction([ta,tb,tb], tb)),

		//  __with  __mutassign   __withassign

		Pair("__cast", MiniTypeFunction([ta, tb], tb)),

		// References
		Pair("__ref", MiniTypeFunction([ta], reftype(ta))),
		Pair("__deref", MiniTypeFunction([reftype(ta)], ta)),
		Pair(":=", MiniTypeFunction([ta, tb], v)),

		Pair("__void", MiniTypeFunction([], v)),

		// special
		Pair("__native", MiniTypeFunction([i, ta, s], ta)),

		Pair(".", MiniTypeFunction([ta, s], tb)),

		//
		// The following special functions are all constructed
		// by Forth builtins
		//

		// Forth "structdef" uses this to build the constructor
		Pair("__construct0", MiniTypeFunction([s], ta)),
		Pair("__construct1", MiniTypeFunction([s, ta], tb)),
		Pair("__construct2", MiniTypeFunction([s, ta, tb], tc)),
		Pair("__construct3", MiniTypeFunction([s, ta, tb, tc], td)),
		Pair("__construct4", MiniTypeFunction([s, ta, tb, tc, td], te)),
	])
}

getBuiltinPureNames() -> Set<string> {
	names = buildSet(getTreeKeys(getBuiltinMiniTypes()));

	impure = buildSet([
		"__cast",
		"__ref", 
		"__deref",
		":=", 
	]);

	differenceSets(names, impure);
}
